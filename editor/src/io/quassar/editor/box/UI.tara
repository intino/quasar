dsl Konos

use Forge
use Landing
use Editors
use Models
use Languages
use Theme

Service EditorElements as UI(port = "{port}", title = "{title}", favicon = "/icons/favicons/favicon.ico")
	Authentication(by="{federation-url}")

	Resource(path = "/") home as Page(homeTemplate) Main > Parameter dialog
	Resource(path = "/about") about as Page(homeTemplate)
	Resource(path = "/languages/:language") language as Page(homeTemplate) > Parameter tab; Parameter view
	Resource(path = "/languages/:language/help") languageReleaseHelp as Page(homeTemplate) > Parameter version
	Resource(path = "/languages/:language/template") languageReleaseTemplate as Page(homeTemplate) > Parameter version
	Resource(path = "/models/:model") model as Page(homeTemplate) > Parameter release; Parameter view; Parameter file; Parameter pos
	Resource(path = "/models/:model/starting") startingModel as Page(homeTemplate)
	Resource(path = "/forge/:model/:release") forge as Page(homeTemplate)
	Resource(path = "/login") login as Page(loginTemplate)
	Resource(path = "/not-found") notFound as Page(notFoundTemplate) > Parameter type
	Resource(path = "/permissions") permissions as Page(permissionsTemplate) > Parameter username; Parameter language; Parameter model; Parameter callback

Template homeTemplate as Relative(height=100%, width=100%)
	TemplateStamp(template=headerTemplate) header

	Block(layout=Vertical Flexible)
		Block(layout=Vertical, format=centered) as Relative(width=100%)
			Block(layout=Vertical Flexible CenterCenter) loading > Spinner(format=secondary)
			Block(layout=Vertical Flexible) landingPage as Conditional > TemplateStamp(template=landingTemplate) landingStamp
			Block(layout=Vertical Flexible) aboutPage as Conditional > TemplateStamp(template=aboutTemplate) aboutStamp
			Block(layout=Vertical Flexible) languagePage as Conditional > TemplateStamp(template=languageTemplate) languageStamp
			Block(layout=Vertical Flexible) modelPage as Conditional > TemplateStamp(template=modelTemplate) modelStamp
			Block(layout=Vertical Flexible) forgePage as Conditional > TemplateStamp(template=forgeTemplate) forgeStamp

Template aboutTemplate as Relative(height=100%, width=100%)
	Block(layout=Vertical Flexible, format=whiteBackground) bodyBlock as Relative(width=100%) AutoSize
		Block(layout=Vertical Flexible Center) contentBlock as Relative(width=80%)
			Block(layout=Vertical Flexible, format=whiteBackground) as Relative(width=100%, height=100%)
				HtmlViewer aboutStamp

Template(layout=Horizontal Center, format=headerStyle) headerTemplate

	Block(layout=Horizontal Center Flexible, format=middleAirTop middleAirBottom middleAirLeft) languageView
		Link(title="", format=logoLink) as Action Addressable(EditorElements.home)
		Image(value="/images/logo-expanded-dark.png", format=airLeft, mobileReduceFactor=75%) as Absolute(width=130px, height=30px)
		Text(value="DSL Forge", visible=false, format=h2 orangeColor bold airLeft) forgeTitle
		Text(value="/", format=whiteColor h2 airLeft airRight, visible=false) languageSeparator
		Text(value="", format=whiteColor h2, visible=false) languageTitleText
		Link(title="", format=whiteColor h2, visible=false) languageTitleLink as Action Addressable(EditorElements.language)

	Block(layout=Horizontal EndJustified)
		MaterialIconButton(title="Set dark mode", format=airRight, icon="Brightness4", size=Small) as SetDarkMode
		MaterialIconButton(title="Set light mode", format=airRight, icon="Brightness7", size=Small) as SetLightMode
		Button(title="About", format=airRight whiteColor) as Action Addressable(EditorElements.about)
		Button(title="Log in", format=airRight whiteColor, visible=false) login as Action

		User(mode=OnlyPhoto, visible=false, format=relative veryLargeIconSize airRight smallAirTop) user
			Selector as ListBox
				Block as Option > Link(title="Projects", visible=false) home as Action Addressable(EditorElements.home)

Template(layout=Vertical Flexible) permissionsTemplate as Relative(height=100%,width=100%)
	Block(layout=Vertical CenterCenter, format=grey) as Relative(height=100%,width=100%)
		Block(format=visible) as Absolute(width=400px, height=400px) Paper
			Block(layout=Vertical Flexible CenterJustified, spacing=DP16, format=relative) as Relative(height=100%)
				Block(layout=Vertical CenterCenter)
					Image(value="/images/logo-expanded.png", format=airRight, mobileReduceFactor=75%, colorInvertedWithDarkMode=true) as Absolute(width=340px, height=80px)
					Text(value="You dont have access permissions", format=h4 airTop textCentered, translate=true) message
			Block
				Divider
				Block(layout=Horizontal CenterJustified, format=middleAirTop)
					Button(title="Login", visible=false, highlight=Outline, format=airRight) loginButton as Action
					Button(title="Logout", visible=false, highlight=Outline) logoutButton as Action

Template(layout=Vertical Flexible) notFoundTemplate as Relative(height=100%,width=100%)
	Block(layout=Vertical CenterCenter, format=grey) as Relative(height=100%,width=100%)
		Block(format=visible) as Absolute(width=400px, height=400px) Paper
			Block(layout=Vertical Flexible CenterJustified, spacing=DP16, format=relative) as Relative(height=100%)
				Block(layout=Vertical CenterCenter)
					Image(value="/images/logo-expanded.png", format=airRight, mobileReduceFactor=75%, colorInvertedWithDarkMode=true) as Absolute(width=340px, height=80px)
					Text(value="Model not found", format=h4 airTop textCentered, translate=true) message
			Block
				Divider
				Block(layout=Horizontal CenterJustified, format=middleAirTop)
					Button(title="Home", highlight=Outline, format=airRight) gotoHome as OpenPage(EditorElements.home)

Template(layout=Vertical Flexible, format=radialBackground) loginTemplate as Relative(height=100%, width=100%)

	Block(layout=Vertical CenterCenter, format=centered) content as Relative(width=80%)
		Block(layout=Horizontal, format=relative)
			Link(title="", format=fullSpaceLink) homeLink as Action
			Image(value="/images/logo-expanded.png", format=airLeft airRight doubleAirBottom, mobileReduceFactor=75%) as Absolute(width=300px, height=70px)
		Block
			Block(layout=Vertical Center) as Absolute(width=400px)
				Block(format=loginBlockStyle)
					Block
						Text(value="Login using...", format=bold, translate=true)
						Selector userSelector as ListBox
						Block(format=doubleAirBottom airLeft airRight) > Text usernameField as Editable(placeholder="new username...")
					Text(value="User not found", translate=true, format=h6 redColor fullWidth textCentered airBottom, visible=false) notFoundUsernameMessage

				Block(layout=Horizontal, format=doubleAirTop)
					Button(title="Enter", highlight=Fill) login as Action

	TemplateStamp(template=loginSelectorOption, visible=false)

Template(layout=Horizontal Flexible Center) loginSelectorOption
	Block(layout=Vertical Flexible) > Text username
	Block(layout=Horizontal EndJustified) > MaterialIconButton(title="Remove", icon="Clear", size=Small) removeUser as Action