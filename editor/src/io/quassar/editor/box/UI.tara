dsl Konos

use Collections
use Forge
use Landing
use Editors
use Models
use Languages
use Theme

Service EditorElements as UI(port = "{port}", title = "{title}", favicon = "/images/favicon.ico")
	Authentication(by="{federation-url}")

	Resource(path = "/") home as Page(homeTemplate) Main > Parameter dialog
	Resource(path = "/about") about as Page(homeTemplate)
	Resource(path = "/collections/:collection") collection as Page(homeTemplate)
	Resource(path = "/models/:language") language as Page(homeTemplate) > Parameter tab
	Resource(path = "/models/:language/:action") languageAction as Page(homeTemplate) > Parameter version
	Resource(path = "/models/:language/:model/:release") model as Page(homeTemplate) > Parameter tab; Parameter view; Parameter file; Parameter pos
	Resource(path = "/forge/:model/:release") forge as Page(homeTemplate) > Parameter view
	Resource(path = "/login") login as Page(loginTemplate)

Template homeTemplate as Relative(height=100%, width=100%)
	TemplateStamp(template=headerTemplate) header

	Block(layout=Vertical Flexible)
		Block(layout=Vertical, format=centered) as Relative(width=100%)
			Block(layout=Vertical Flexible CenterCenter) loading > Spinner(format=secondary)
			Block(layout=Vertical Flexible) landingPage as Conditional > TemplateStamp(template=landingTemplate) landingStamp
			Block(layout=Vertical Flexible) aboutPage as Conditional > TemplateStamp(template=aboutTemplate) aboutStamp
			Block(layout=Vertical Flexible) collectionPage as Conditional > TemplateStamp(template=collectionTemplate) collectionStamp
			Block(layout=Vertical Flexible) languagePage as Conditional > TemplateStamp(template=languageTemplate) languageStamp
			Block(layout=Vertical Flexible) modelPage as Conditional > TemplateStamp(template=modelTemplate) modelStamp
			Block(layout=Vertical Flexible) forgePage as Conditional > TemplateStamp(template=forgeTemplate) forgeStamp

Template aboutTemplate as Relative(height=100%, width=100%)
	Block(layout=Vertical Flexible, format=whiteBackground autoHeight doubleBottomSpaced) bodyBlock as Relative(width=100%) AutoSize
		Block(layout=Vertical Flexible Center) contentBlock as Relative(width=80%)
			Block(layout=Vertical Flexible, format=whiteBackground) as Relative(width=50%)
				HtmlViewer aboutStamp

Template(layout=Horizontal Center, format=headerStyle) headerTemplate

	Block(layout=Horizontal Center Flexible, format=middleAirTop middleAirBottom middleAirLeft) languageView
		Link(title="", format=logoLink) as Action Addressable(EditorElements.home)
		Image(value="/images/logo-expanded-dark.png", format=airLeft, mobileReduceFactor=75%) as Absolute(width=130px, height=30px)
		Block(layout=Horizontal Center, visible=false) languageBlock
			Text(value=" ", format=whiteColor h3 headerBullet airLeft airRight middleAirTop)
			Link(title="", format=whiteColor h3) languageLink as Action

	Block(layout=Horizontal EndJustified)
		MaterialIconButton(title="Set dark mode", format=airRight, icon="Brightness4", size=Small) as SetDarkMode
		MaterialIconButton(title="Set light mode", format=airRight, icon="Brightness7", size=Small) as SetLightMode
		//Button(title="About", format=airRight whiteColor) as Action Addressable(EditorElements.about)
		Button(title="Log in", format=airRight whiteColor, visible=false) login as Action

		User(mode=OnlyPhoto, visible=false, format=relative veryLargeIconSize airRight smallAirTop) user
			Block(format=spaced) > TemplateStamp(template=userHomeTemplate) userHomeStamp
//			Selector userLinksSelector as ListBox
//				Block(visible=false) myProjectsOption as Option > Link(title="Projects") myProjects as Action Addressable(EditorElements.home)

Template userHomeTemplate

	Block(format=doubleAirBottom)
		Text(value="My licenses", translate=true, format=h5 smallAirBottom)
		TemplateStamp(template=licenseItemTemplate) licenses as Multiple(noItemsMessage="You currently have no licenses")
		Block > TemplateStamp(template=addLicenseEditor) addLicenseEditor

	Block(format=doubleAirBottom, visible=false) myCollectionsBlock
		Text(value="My collections", translate=true, format=h5)
		TemplateStamp(template=collectionItemTemplate) collections as Multiple(noItemsMessage="You are not currently managing any collections")

	TemplateStamp(template=renewLicenseDialog) renewLicenseDialog

Template(format=middleAirBottom greyLightBackground middleSpaced middleRounded) licenseItemTemplate
	Block(layout=Horizontal Center) as HoverContainer
		Block(layout=Vertical Flexible)
			Block(layout=Horizontal) > Text(value=" ") bullet; Text title
			Text(format=greyColor) expirationInfo
		Block(layout=Horizontal EndJustified, format=middleAirRight, hidden=IfNotHover)
			Link(title="renew", visible=false) renew as Action

Template collectionItemTemplate
	Link(title="") collectionLink as Action Addressable(EditorElements.collection)

Template(layout=Vertical Flexible, format=radialBackground) loginTemplate as Relative(height=100%, width=100%)

	Block(layout=Vertical CenterCenter, format=centered) content as Relative(width=80%)
		Block(layout=Horizontal, format=relative)
			Link(title="", format=fullSpaceLink) homeLink as OpenSite(site=empty)
			Image(value="/images/logo-expanded.png", format=airLeft airRight doubleAirBottom, mobileReduceFactor=75%) as Absolute(width=180px, height=41px)
		Block
			Block(layout=Vertical Center) as Absolute(width=400px)

				Block(format=googleLoginBlockStyle doubleAirBottom, visible=false) googleLoginBlock
					Block(layout=Vertical Center)
						Text(value="Login using Google", format=bold h6, translate=true)
						Block(format=spaced) > DisplayStamp(display=googleLoginDisplay) googleLoginStamp

				Block(layout=Vertical Center, visible=false) localLoginBlock as Relative(width=100%)
					Block(layout=Vertical Flexible Center) as Relative(width=100%)
						Text(value="Login using...", format=h6 bold blackColor airBottom, translate=true)
						Block(format=localLoginBlockStyle)
							Selector(format=userSelectorStyle) userSelector as ListBox
							Block(format=doubleAirBottom airLeft airRight) > Text usernameField as Editable(placeholder="new username...")
							Text(value="User not found", translate=true, format=h6 redColor fullWidth textCentered airBottom, visible=false) notFoundUsernameMessage

					Block(layout=Horizontal, format=doubleAirTop)
						Button(title="Enter", highlight=Fill, format=whiteButton) login as Action

	TemplateStamp(template=loginSelectorOption, visible=false)

Template(layout=Horizontal Flexible Center) loginSelectorOption
	Block(layout=Vertical Flexible) > Text username
	Block(layout=Horizontal EndJustified) > MaterialIconButton(title="Remove", icon="Clear", size=Small) removeUser as Action

Template(layout=Vertical) licenseExpiredBanner
	Block(visible=false, format=banner) content
		Block(layout=Vertical Flexible)
			Block(layout=Horizontal) > Text(value=" ") bullet; Text title
			Text(format=greyColor) expirationInfo
			Text(format=airRight) expirationMessage
		Block(layout=Horizontal, format=middleAirTop) > Button(title="Renew", size=Small, highlight=Outline) renewLicense as Action
		Block(format=whiteBackground, visible=false) addLicenseBlock > TemplateStamp(template=addLicenseEditor) addLicenseEditor

	TemplateStamp(template=renewLicenseDialog) renewLicenseDialog

Display googleLoginDisplay
	Notification refresh as Text

	Request success as Text
	Request failure