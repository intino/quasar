dsl Konos

use Editors
use Models
use UI
use Theme

Template(layout=Horizontal Center, format=subHeaderStyle) languageHeaderTemplate

	Block(layout=Horizontal Flexible)
		Block(layout=Horizontal) as Relative(width=100%)
			Text(value="models", format=doubleAirRight bold) modelsText
			Link(title="models", format=doubleAirRight, visible=false) modelsLink as Action Addressable(EditorElements.language)
			Text(value="examples", format=doubleAirRight bold, visible=false) examplesText
			Link(title="examples", format=doubleAirRight, visible=false) examplesLink as Action Addressable(EditorElements.language)

	Block(layout=Horizontal EndJustified)
		Link(title="metamodel", format=doubleAirRight, visible=false) metamodelLink as Action Addressable(EditorElements.model)
		Text(value="help", format=doubleAirRight bold, visible=false) helpText
		Link(title="help", format=doubleAirRight, visible=false) helpLink as Action Addressable(EditorElements.language)
		Text(value="about", format=doubleAirRight bold) aboutText
		Link(title="about", format=doubleAirRight, visible=false) aboutLink as Action Addressable(EditorElements.language)

Template languagesTemplate as Relative(height=100%, width=100%)

	Block(layout=Horizontal Center)
		Text(visible=false, format=airTop doubleAirLeft doubleAirRight airBottom) searchBox as Editable(placeholder="Search...")

	Block(layout=Vertical Flexible) bodyBlock as Relative(width=100%) AutoSize
		Block(layout=Vertical Flexible Center) contentBlock as Relative(width=100%)
			List(itemClass="io.quassar.editor.model.Language", pageSize=20) languagesCatalog
				noItemsMessage = "No DSLs"
				Mold
					Item(height=100px) languageItem
						Block(layout=Horizontal Flexible, format=doubleBottomSpaced topSpaced bottomBordered)
							Block(layout=Vertical Center) as Absolute(width=100px) > Image(colorInvertedWithDarkMode=true) logo as Absolute(width=60px)
							Block(layout=Vertical Flexible)
								Link(title="", format=h4 upperCase, visible=false) name as Action Addressable(EditorElements.language)
								Link(title="", format=h4 upperCase, visible=false) nameSelector as Action
								Text(format=h6 bold) title
								Text description

Template languagesLandingTemplate as Relative(height=100%, width=100%)

	Block(layout=Horizontal Center)
		Text(visible=false, format=airTop doubleAirLeft doubleAirRight airBottom) searchBox as Editable(placeholder="Search...")

	Block(layout=Vertical Flexible) bodyBlock as Relative(width=100%) AutoSize
		Block(layout=Vertical Flexible Center) contentBlock as Relative(width=100%)
			List(itemClass="io.quassar.editor.model.Language", pageSize=5) languagesLandingCatalog as Navigable(position=Bottom)
				noItemsMessage = "No DSLs"
				Mold
					Item(height=100px) LanguageLandingItem
						Block(layout=Horizontal Flexible, format=doubleBottomSpaced topSpaced bottomBordered)
							Block(layout=Vertical Center) as Absolute(width=100px) > Image(colorInvertedWithDarkMode=true) logo as Absolute(width=60px)
							Block(layout=Vertical Flexible)
								Link(title="", format=h4 upperCase, visible=false) name as Action Addressable(EditorElements.language)
								Link(title="", format=h4 upperCase, visible=false) nameSelector as Action
								Text(format=h6 bold) title
								Text description

Template languageTemplate as Relative(height=100%, width=100%, offsetHeight=2px)
	TemplateStamp(template=languageHeaderTemplate, visible=false) headerStamp

	Block(layout=Vertical Flexible CenterCenter, visible=false) openingBlock
		Spinner(mode=Ring, color="secondary")
		Text(value="" , translate=true, format=h3 airTop) openingMessage

	Block(layout=Vertical Flexible, format=whiteBackground) bodyBlock as Relative(width=100%)

		Block(layout=Vertical Flexible CenterCenter, visible=false) notFoundBlock
			Block(layout=Vertical Flexible CenterJustified, spacing=DP16, format=relative) as Relative(height=100%)
				Block(layout=Vertical CenterCenter)
					Image(value="/images/logo-expanded.png", format=airRight, mobileReduceFactor=75%) as Absolute(width=340px, height=80px)
					Text(value="Language not found", format=h4 airTop textCentered, translate=true) message

		Block(layout=Vertical Flexible, format=whiteBackground) helpBlock as Relative(width=100%) AutoSize Conditional
			Block(layout=Vertical Flexible Center) as Relative(width=80%)
				Block(layout=Vertical Flexible, format=whiteBackground) as Relative(width=100%, height=100%)
					Block(layout=Horizontal Center, format=bottomLightBordered middleTopSpaced middleBottomSpaced)
						Block(layout=Horizontal Flexible)
							Text(format=h2) helpTitle
						Block(layout=Horizontal EndJustified, format=doubleAirRight)
							Image(format=airRight, mobileReduceFactor=75%, colorInvertedWithDarkMode=true) helpLogo as Absolute(height=50px)
					HtmlViewer helpStamp

		Block(layout=Vertical Flexible) mainBlock as Relative(width=100%) Conditional

			Block(layout=Horizontal Flexible, format=whiteBackground) mainContentBlock as Relative(width=100%, height=100%) Resizable

				Block(layout=Vertical Flexible, format=doubleLeftSpaced doubleRightSpaced bottomSpaced topSpaced) modelsBlock as Relative(width=65%)
					TemplateStamp(template=modelsTemplate) modelsStamp

				Block(layout=Vertical Flexible) homeBlock as Relative(width=35%)
					Block(layout=Horizontal Flexible Center, format=bottomLightBordered middleTopSpaced middleBottomSpaced doubleLeftSpaced)
						Block(layout=Horizontal Flexible)
							Text(format=h2) title
						Block(layout=Horizontal EndJustified, format=doubleAirRight)
							Image(format=airRight, mobileReduceFactor=75%, colorInvertedWithDarkMode=true) logo as Absolute(height=50px)

					Block(layout=Vertical Flexible, format=doubleAirTop doubleLeftSpaced doubleRightSpaced doubleBottomSpaced) viewsBlock as AutoSize
						Block(visible=false) aboutBlock
							Block(format=doubleAirBottom)
								Text(value="title", format=h6 bold)
								Text aboutTitle
							Block(format=doubleAirBottom)
								Text(value="what is it", format=h6 bold)
								Text aboutDescription
							Block(format=doubleAirBottom)
								Text(value="how to cite", format=h6 bold)
								Text aboutCitation
							Block(format=doubleAirBottom)
								Text(value="copyright and license", format=h6 bold)
								Text aboutLicense

						Block(visible=false) versionsBlock
							Block(format=doubleAirBottom)
								Text(value="versions", format=h6 bold)
								TemplateStamp(template=languageReleaseHelp) helps as Multiple(noItemsMessage="No help available")

Template languageForgeTemplate as Relative(height=100%, width=100%)
	Block(layout=Horizontal Flexible) as Relative(height=100%, width=100%) Resizable
		Block(format=doubleSpaced) as Relative(width=30%)
			Block(layout=Vertical Flexible) > TemplateStamp(template=languageInfoTemplate) infoStamp
		Block(layout=Vertical Flexible) as Relative(width=70%)
			Selector viewSelector as Tabs(format=tabsStyle) Addressable(EditorElements.forge)
				Text(value="Properties") properties as Option
				Text(value="Help") help as Option
				Text(value="Kit") kit as Option
				Text(value="Tools") tools as Option

			Block(layout=Horizontal Flexible)
				Block(layout=Vertical Flexible) as Relative(width=90%)
					Block(layout=Vertical Flexible) propertiesBlock as Conditional(viewSelector.properties)
						TemplateStamp(template=languagePropertiesTemplate) propertiesStamp
					Block(layout=Vertical Flexible) helpBlock as Conditional(viewSelector.help)
						TemplateStamp(template=languageHelpTemplate) helpStamp
					Block(layout=Vertical Flexible) kitBlock as Conditional(viewSelector.kit)
						TemplateStamp(template=languageKitTemplate) kitStamp
					Block(layout=Vertical Flexible) toolsBlock as Conditional(viewSelector.tools)
						TemplateStamp(template=languageToolsTemplate) toolsStamp
				Block(format=greyBackground, visible=false) versionSelectorBlock as Relative(width=10%)
					Text(value="Versions", translate=true, format=h5 bold middleAirTop middleAirLeft middleAirBottom)
					Selector versionSelector as ListBox Addressable(EditorElements.forge)

Template(layout=Vertical Flexible) languageInfoTemplate
	Block(layout=Vertical, format=doubleAirBottom relative)
		TemplateStamp(template=languageEditor) editorStamp
	Block(format=doubleAirBottom)
		Text(value="Metamodel", translate=true, format=h5 bold middleAirBottom)
		Block(layout=Horizontal) > Link(title="") metamodelLink as OpenSite(site=empty, target=Blank)
	Block(format=doubleAirBottom)
		Text(value="Visibility", translate=true, format=h5 bold middleAirBottom)
		Block(layout=Horizontal)
			Selector visibilitySelector as ComboBox
				Text(value="Public") publicVisibilityOption as Option
				Text(value="Private") privateVisibilityOption as Option
	Block(visible=false) publicVisibilityBlock
		Block(format=doubleAirBottom doubleAirTop)
			Text(value="Restrict access to only this email accounts", translate=true, format=h5 bold middleAirBottom)
			Text(value="List of patterns separated by semicolon with access to this language", translate=true, format=message middleAirBottom)
			Text grantAccessField as Editable Memo(height=5Rows)
		Block(format=doubleAirBottom doubleAirTop)
			Text(value="Copyright and license", translate=true, format=h5 bold middleAirBottom)
			Text licenseField as Editable Memo(height=5Rows)

Template(layout=Vertical Flexible, format=doubleSpaced) languagePropertiesTemplate
	Block(format=doubleAirBottom)
		Text(value="Title", translate=true, format=h5 bold middleAirBottom)
		Text titleField as Editable
	Block(format=doubleAirBottom)
		Text(value="What is it", translate=true, format=h5 bold middleAirBottom)
		Text descriptionField as Editable Memo(height=5Rows)
	Block(format=doubleAirBottom)
		Text(value="Acknowledgement", translate=true, format=h5 bold middleAirBottom)
		Text acknowledgementField as Editable Memo(height=5Rows)
	Block(format=doubleAirBottom)
		Text(value="Credits", translate=true, format=h5 bold middleAirBottom)
		Text creditsField as Editable Memo(height=5Rows)
	Block(format=doubleAirBottom)
		Text(value="How to cite", translate=true, format=h5 bold middleAirBottom)
		Text citationField as Editable Memo(height=5Rows)
	Block(format=doubleAirBottom)
		Text(value="Tags", translate=true, format=h5 bold middleAirBottom)
		TemplateStamp(template=tagEditor) tags as Multiple("No tags", arrangement=Horizontal, spacing=DP8)
		Block(layout=Horizontal, format=airTop) > Button(title="Add tag", size=Small, highlight=Outline) as OpenDialog(addTagDialog)

	Dialog(title="Add tag") addTagDialog as Absolute(width=280px, height=200px)
        Block as Relative(height=100%, offsetHeight=40px)
        	Text tagField as Editable(placeholder="Indicate tag name") Labeled("Tag") Focused
		Divider
        Block(layout=Horizontal EndJustified)
            Button(title="Cancel", size=Small, format=airRight) as CloseDialog(addTagDialog)
            Button(title="Accept", size=Small, highlight=Fill) addTag as Action

Template(layout=Horizontal Flexible) languageHelpTemplate
	Block(layout=Vertical Flexible, visible=false) selectVersionBlock
		Text(value="Select version on right panel to open help")

	Block(layout=Vertical Flexible CenterCenter, visible=false) versionNotCreatedBlock
		Block(layout=Vertical CenterCenter) as Relative(width=40%)
			Text(value="This version of the language has not been defined yet. To define its help, you must first create the version.", format=h5 textCentered)
			Button(title="Create version", highlight=Fill, format=doubleAirTop) createVersion as Action

	Block(layout=Vertical Flexible, visible=false) versionBlock
		Text(format=fullHeight fullWidth) helpField as Editable Code(Html)

Template(layout=Vertical Flexible, format=doubleSpaced) languageKitTemplate
	Block(layout=Vertical Flexible, visible=false) selectVersionBlock
		Text(value="Select version on right panel to open kit")

	Block(layout=Vertical Flexible CenterCenter, visible=false) versionNotCreatedBlock
		Block(layout=Vertical CenterCenter) as Relative(width=40%)
			Text(value="This version of the language has not been defined yet. To define its kit, you must first create the version.", format=h5 textCentered)
			Button(title="Create version", highlight=Fill, format=doubleAirTop) createVersion as Action

	Block(layout=Vertical Flexible, visible=false) versionBlock
		Block(format=doubleAirBottom)
			Text(value="Model readers", translate=true, format=h5 bold)
			TemplateStamp(template=modelReaderTemplate) readers as Multiple(arrangement=Horizontal, spacing=DP16, noItemsMessage="There are no readers available")

		Block(format=doubleAirBottom)
			Text(value="Template", translate=true, format=h5 bold)
			Block(visible=false) templateDefined
				Link(title="") templateLink as OpenSite(site=empty, target=Blank)
				Date templateCreateDate as Labeled("Created at")
			Block(layout=Horizontal, visible=false) templateNotDefined
				Button(title="Create", highlight=Outline) createTemplate as Action

		Block(layout=Vertical Flexible) as Relative(height=100%)
			Text(value="Examples", translate=true, format=h5 bold middleAirBottom)
			TemplateStamp(template=modelsTemplate) modelsCatalog

Template(layout=Vertical Flexible, format=doubleSpaced) languageToolsTemplate
	Block(layout=Vertical Flexible, visible=false) selectVersionBlock
		Text(value="Select version on right panel to open tools")

	Block(layout=Vertical Flexible CenterCenter, visible=false) versionNotCreatedBlock
		Block(layout=Vertical CenterCenter) as Relative(width=40%)
			Text(value="This version of the language has not been defined yet. To define its tools, you must first create the version.", format=h5 textCentered)
			Button(title="Create version", highlight=Fill, format=doubleAirTop) createVersion as Action

	Block(layout=Vertical Flexible, visible=false) versionBlock
		Text(value="Specify how a model, written with this DSL, is converted into a concrete artifact.", translate=true, format=airBottom)
		Text(value="Available tools", translate=true, format=h5 airBottom)
		TemplateStamp(template=languageToolTemplate) tools as Multiple(noItemsMessage="No tools defined", spacing=DP16)
		Link(title="[add]", format=airTop) as OpenDialog(dialog)

	Dialog(title="Add tool") dialog as Relative(width=60%, height=60%)
        Block content as Relative(height=100%, offsetHeight=45px)
			Wizard(position=Top, style=ContentOnly, format=fullWidth) wizard
				Step step1
					Block(format=setupStepHeader)
						Text(value="Tool name", format=setupStepTitle, translate=true)
						Text(value="Set the name for the tool", format=setupStepSubtitle, translate=true)
					Block(format=setupStepContent)
						Text(format=airBottom) toolNameField as Editable("Enter the tool name") Focused
				Step step2 as Labeled("")
					Block(format=setupStepHeader)
						Text(value="Tool type", format=setupStepTitle, translate=true)
						Text(value="Indicate the type of tool that will be created to deploy the model built by the user", format=setupStepSubtitle, translate=true)
					Block(format=setupStepContent)
						Selector toolTypeSelector as ListBox
							Text(value="Docker image factory") dockerImageFactoryOption as Option
							Text(value="Deploy site factory") deploySiteFactoryOption as Option
							Text(value="Build instructions") buildInstructionsOption as Option
				Step step3
					Block(format=setupStepHeader)
						Text(value="Setup tool", format=setupStepTitle, translate=true)
						Text(value="Configure the selected tool type", format=setupStepSubtitle, translate=true)
					Block(format=setupStepContent)
						TemplateStamp(template=languageToolEditor) toolEditor
		Divider
        Block(layout=Horizontal EndJustified)
            WizardNavigator(wizard=content.wizard)

Template languageToolTemplate
	Text(format=bold) name
	TemplateStamp(template=languageToolParameter) parameters as Multiple(noItemsMessage="No parameters")
	Link(title="[remove]") removeLink as Action Affirmed("Confirm that you want to delete the tool")

Template(layout=Horizontal) languageToolParameter
	Text(format=grey airRight) name
	Text value

Template languageToolEditor

	Block dockerImageFactoryBlock as Relative(height=100%, width=100%) Conditional
		Block(format=doubleAirBottom)
			Text(value="Docker image url", translate=true)
			Text(value="Use [model] or [release] to indicate current model/release version", format=description, translate=true)
			Text(format=airBottom) dockerImageField as Editable Focused
		Block
			Text(value="Docker image run parameters", translate=true)
			Text(value="Use [model] or [release] to indicate current model/release version", format=description, translate=true)
			Text(format=airBottom) dockerImageRunField as Editable

	Block deploySiteFactoryBlock as Relative(height=100%, width=100%) Conditional
		Block
			Text(value="Deploy site url. It must contain two parameters: [model] and [release]", translate=true)
			Text(format=airBottom) siteUrlField as Editable(placeholder="https://[your-site]?model=[model]&release=[release]") Focused

	Block buildInstructionsBlock as Relative(height=100%, width=100%) Conditional
		Block(format=doubleAirBottom)
			Text(value="Install instructions", translate=true)
			Text(format=airBottom) installInstructionsField as Editable Memo(height=4Rows) Focused
		Block
			Text(value="Execution instructions", translate=true)
			Text(format=airBottom) executionInstructionsField as Editable Memo(height=4Rows)

Template modelReaderTemplate
	Link(title="") titleLink as Download

Template languageReleaseHelp
	Link(title="") titleLink as OpenSite(site=empty, target=Blank)