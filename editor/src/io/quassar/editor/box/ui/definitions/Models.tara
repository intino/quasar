dsl Konos

use Editors
use UI
use Theme

Template modelsTemplate as Relative(height=100%, width=100%)

	Block(layout=Horizontal Center, format=middleAirBottom)
		Button(title="New", size=Small, highlight=Outline, visible=false) addModelTrigger as Action
		SearchBox(collections=body.modelList) searchBox

	Block(layout=Vertical Flexible) body
		List(itemClass="io.quassar.editor.model.Model") modelList
			noItemsMessage = "No models"
			Mold
				Item(height = 70px) modelItem
					Link(title="", format=relative bringToFront h5) label as Action Addressable(EditorElements.model)
					Text description
					Text owner as Labeled("Owner")
					Date createDate as Labeled("Create date")

	TemplateStamp(template=addModelDialog) addModelDialog

Template modelTemplate as Relative(height=100%, width=100%)
	TemplateStamp(template=modelHeaderTemplate) headerStamp

	Block(layout=Vertical Flexible CenterCenter, visible=false) openingModelBlock
		Spinner(mode=Ring, color="secondary")
		Text(value="" , translate=true, format=h3 airTop) openingModelMessage

	Block(layout=Vertical Flexible CenterCenter, visible=false) notFoundBlock
		Block(layout=Vertical Flexible CenterJustified, spacing=DP16, format=relative) as Relative(height=100%)
			Block(layout=Vertical CenterCenter)
				Image(value="/logo-expanded.png", format=airRight, mobileReduceFactor=75%) as Absolute(width=340px, height=80px)
				Text(value="Model not found", format=h4 airTop textCentered, translate=true) message

	Block(layout=Horizontal Flexible, visible=false) contentBlock as Resizable

		Block(layout=Horizontal Flexible) as Relative(width=20%)
			Block(format=tabPanel leftTabPanel autoWidth) tabSelectorBlock as Relative(height=100%)
				Block(format=panelDrawer) tabSelectorDrawer as Drawer(variant=PersistentAndMini) Absolute(width=200px)
					Block(layout=Horizontal Center, format=middleAirLeft middleAirRight middleAirTop) as Absolute(height=30px)
						Block(layout=Horizontal EndJustified Center) as Absolute(height=30px)
							MaterialIconButton(title="open menu", icon="ChevronRight", size=Small, format=airLeft) as OpenDrawer(drawer=tabSelectorDrawer)
							MaterialIconButton(title = "minimize", icon="ChevronLeft", size=Small, format=airLeft) as CloseDrawer(drawer=tabSelectorDrawer)
							Text(value="Tools", format=airLeft, translate=true)
					Divider
					Block(layout=Vertical Flexible) views
						Selector tabSelector as ListBox
							Block(layout=Horizontal Center) modelOption as Option > MaterialIcon(icon="DescriptionOutlined", title="Model", format=airRight); Text(value="Model", translate=true)
							Block(layout=Horizontal Center) resourcesOption as Option > MaterialIcon(icon="AccountTreeOutlined", title="Resources", format=airRight); Text(value="Resources", translate=true)

			Block(layout=Vertical Flexible, format=whiteBackground) browserBlock as Relative(width=100%, height=100%)
				Block(layout=Vertical Flexible) modelBrowserBlock as Conditional(tabSelectorBlock.tabSelectorDrawer.views.tabSelector.modelOption) Relative(height=100%, width=100%) > TemplateStamp(template=modelBrowserTemplate) modelBrowserStamp
				Block(layout=Vertical Flexible) resourcesBrowserBlock as Conditional(tabSelectorBlock.tabSelectorDrawer.views.tabSelector.resourcesOption) Relative(height=100%, width=100%) > TemplateStamp(template=resourcesBrowserTemplate) resourcesBrowserStamp

		Block(layout=Vertical Flexible) as Relative(width=80%)
			Block(layout=Horizontal, format=leftBordered headerPanel greyLightBackground leftSpaced rightSpaced)
				Block(layout=Horizontal Flexible)
					Text(value="", format=h6, translate=true) filename
					Block(layout=Horizontal, visible=false, format=airLeft) fileModifiedMessage > Text(value="modified", translate=true, format=airRight redColor bold)
					Block(layout=Horizontal, visible=false, format=airLeft) fileSavedMessage > Text(value="saved", translate=true, format=airRight greenColor bold)
				Block(layout=Horizontal EndJustified)
			Block(layout=Vertical Flexible CenterCenter, format=whiteBackground) fileNotSelectedBlock
				Text(value="Select file on left panel to open it")
			Block(layout=Vertical Flexible, visible=false) fileSelectedBlock
				DisplayStamp(display=intinoDslEditor, format=fullHeight) intinoDslEditor
			Block(visible=false) consoleBlock > TemplateStamp(template=consoleTemplate) console

	Dialog(title="File modified") fileModifiedDialog as Absolute(width=500px, height=180px)
        Block as Relative(height=100%, offsetHeight=40px)
        	Text(value="File have been modified. What do you want to do?", translate=true)
		Divider
		Block(layout=Horizontal)
			Block(layout=Horizontal Flexible StartJustified)
				Button(title="Cancel", size=Small, format=airRight) cancelSavingFile as Action
			Block(layout=Horizontal EndJustified)
				Button(title="Don't save", size=Small, format=airRight) continueWithoutSavingFile as Action
				Button(title="Save", size=Small, highlight=Fill) continueSavingFile as Action

Template(layout=Vertical Flexible) modelBrowserTemplate as Relative(height=100%, width=100%)

	Block(layout=Horizontal, format=headerPanel greyLightBackground leftSpaced rightSpaced)
		Block(layout=Horizontal Flexible) > Text(value="Model", format=h6, translate=true)
		Block(layout=Horizontal Center, format=smallAirLeft)
			MaterialIconButton(title="Operations...", icon="MoreVert", size=Small) operationsTrigger as OpenPopover(operationsDialog)

	DisplayStamp(display=intinoFileBrowser, format=displayHeaderPanel smallAirRight) fileBrowser

	Block(format=spaced) operationsDialog as Popover Absolute(width=250px)
		Block(layout=Vertical Start)
			Link(title="Add model file", format=middleAirBottom) addFileTrigger as OpenDialog(fileDialog) > Shortcut(key="N", metaKeys=Control, visible=true)
			Link(title="Add folder", format=middleAirBottom) addFolderTrigger as OpenDialog(fileDialog) > Shortcut(key="N", metaKeys=Shift Control, visible=true)
			Link(title="Rename", format=middleAirBottom) editFilenameTrigger as OpenDialog(fileDialog) Readonly > Shortcut(key="R", metaKeys=Control, visible=true)
			Link(title="Remove", format=middleAirBottom) removeFileTrigger as Action Affirmed("Are you sure to remove?") Readonly

	Dialog(title="") fileDialog as Absolute(width=400px, height=200px)
        Block(layout=Horizontal) as Relative(height=100%, offsetHeight=40px)
			Text fileField as Editable(placeholder="Indicate name") Labeled("Name") Focused
		Divider
        Block(layout=Horizontal EndJustified)
            Button(title="Cancel", size=Small, format=airRight) as CloseDialog(fileDialog)
            Button(title="Accept", size=Small, highlight=Fill) addFile as Action Readonly

Template(layout=Vertical Flexible) resourcesBrowserTemplate as Relative(height=100%, width=100%)

	Block(layout=Horizontal, format=headerPanel greyLightBackground leftSpaced rightSpaced)
		Block(layout=Horizontal Flexible) > Text(value="Resources", format=h6, translate=true)
		Block(layout=Horizontal Center, format=smallAirLeft)
			MaterialIconButton(title="Operations...", icon="MoreVert", size=Small) operationsTrigger as OpenPopover(operationsDialog)

	DisplayStamp(display=intinoFileBrowser, format=displayHeaderPanel smallAirRight) fileBrowser

	Block(format=spaced) operationsDialog as Popover Absolute(width=250px)
		Block(layout=Vertical Start)
			Link(title="Add file", format=middleAirBottom) addFileTrigger as OpenDialog(fileDialog) > Shortcut(key="N", metaKeys=Control, visible=true)
			Link(title="Add folder", format=middleAirBottom) addFolderTrigger as OpenDialog(fileDialog) > Shortcut(key="N", metaKeys=Shift Control, visible=true)
			Link(title="Rename", format=middleAirBottom) editFilenameTrigger as OpenDialog(fileDialog) Readonly > Shortcut(key="R", metaKeys=Control, visible=true)
			Link(title="Remove", format=middleAirBottom) removeFileTrigger as Action Affirmed("Are you sure to remove?") Readonly

	Dialog(title="") fileDialog as Absolute(width=600px, height=350px)
        Block(layout=Horizontal) as Relative(height=100%, offsetHeight=40px)
        	Block(format=greyLightBackground) selectorBlock as Absolute(width=150px)
				Selector fileDialogSelector as ListBox
					Text(value="New file") newFileOption as Option
					Text(value="Upload file") uploadFileOption as Option
			Block(layout=Vertical Flexible, format=airLeft)
				Block(format=airTop) fileFieldBlock as Conditional(selectorBlock.fileDialogSelector.newFileOption)
					Text fileField as Editable(placeholder="Indicate name") Labeled("Name") Focused
				Block(format=airTop airBottom) fileUploadBlock as Conditional(selectorBlock.fileDialogSelector.uploadFileOption)
					File fileUploadField as Editable(showDropZone=true)
		Divider
        Block(layout=Horizontal EndJustified)
            Button(title="Cancel", size=Small, format=airRight) as CloseDialog(fileDialog)
            Button(title="Accept", size=Small, highlight=Fill) addFile as Action Readonly

Display intinoFileBrowser
	Notification refresh as Object(intinoFileBrowserInfo)
	Notification select as Object(intinoFileBrowserItem)
	Request open as Text
	Request rename as Object(intinoFileBrowserRenameInfo)
	Request move as Object(intinoFileBrowserMoveInfo)

Schema intinoFileBrowserInfo
	Attribute itemAddress as Text
	Attribute rootItem as Text
	Attribute items as Object(intinoFileBrowserItem) List

Schema intinoFileBrowserRenameInfo
	Attribute id as Integer
	Attribute newName as Text

Schema intinoFileBrowserMoveInfo
	Attribute file as Integer
	Attribute directory as Text

Schema intinoFileBrowserItem
	Attribute id as Integer
	Attribute name as Text
	Attribute uri as Text
	Attribute type as Word("Folder" "File")
	Attribute isRoot as Bool
	Attribute parents as Text List
	Attribute children as Text List

Display intinoDslEditor
	Notification setup as Object(intinoDslEditorSetup)
	Notification refresh as Object(intinoDslEditorFile)
	Notification receiveContent
	Request fileModified
	Request fileContent as Text

Schema intinoDslEditorSetup
	Attribute dslName as Text
	Attribute modelName as Text
	Attribute modelRelease as Text
	Attribute readonly as Bool

Schema intinoDslEditorFile
	Attribute name as Text
	Attribute model as Text
	Attribute uri as Text
	Attribute content as Text
	Attribute language as Text
	Attribute extension as Text

Template(format=consoleStyle) consoleTemplate
	MaterialIconButton(title="Close", icon="Clear", size=Small, format=closeIcon) closeButton as Action
	Text(value="Build output", format=h6 airLeft middleAirTop, translate=true)
	Block(format=airTop airLeft airRight middleAirBottom)
		TemplateStamp(template=consoleEntryTemplate) entries as Multiple(noItemsMessage="No messages")

Template(layout=Horizontal Center, format=middleAirBottom) consoleEntryTemplate
	MaterialIcon(icon="Info", format=smallIconSize airRight blueColor, visible=false) infoIcon
	MaterialIcon(icon="Warning", format=smallIconSize airRight orangeColor, visible=false) warningIcon
	MaterialIcon(icon="Error", format=smallIconSize airRight redColor, visible=false) errorIcon
	Link(title="", format=airRight) file as Action Addressable(EditorElements.model)
	Text(format=airRight) location
	Text content

