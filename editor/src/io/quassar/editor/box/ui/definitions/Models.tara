dsl Konos

use Editors
use UI
use Theme

Template modelsTemplate as Relative(height=100%, width=100%)

	Block(layout=Horizontal)
		Button(title="Add model...", size=Small, highlight=Outline, visible=false) addModelTrigger as Action

	Block(layout=Vertical Flexible) body
		Table(itemClass="io.quassar.editor.model.Model") modelTable
			noItemsMessage = "No models"
			Mold
				Heading modelLabelHeading > Text(value="Title")
				Item(height=30px, width=20%) modelLabelItem > Link(title="", format=relative bringToFront) label as Action Addressable(EditorElements.model)
			Mold
				Heading modelDescriptionHeading > Text(value="Description")
				Item(height=30px, width=45%) modelDescriptionItem > Text description
			Mold
				Heading modelOwnerHeading > Sorting(collections=modelTable) ownerSorting as OrderBy Labeled("Owner")
				Item(height=30px, width=15%) modelOwnerItem > Text owner
			Mold
				Heading operationsHeading > Block(layout=Horizontal EndJustified)
				Item(height=30px, width=20%) operationsItem
					Block(visible=false, format=operationsContainer) operationsToolbar as HoverContainer
						Block(layout=Horizontal Flexible EndJustified, hidden=IfNotHover) as Relative(width=100%)
							MaterialIconButton(icon="Settings", title="Settings", size=Small) settingsModelTrigger as Action
							MaterialIconButton(icon="CloudUpload", title="Publish", size=Small) publishModelTrigger as Action
							MaterialIconButton(icon="Clear", title="Remove", size=Small) removeModelTrigger as Action Affirmed("Are you sure to remove model?")

	TemplateStamp(template=addModelDialog) addModelDialog
	TemplateStamp(template=modelSettingsDialog) modelSettingsDialog
	TemplateStamp(template=publishModelDialog) modelPublishDialog

Template modelTemplate as Relative(height=100%, width=100%)
	TemplateStamp(template=modelHeaderTemplate) headerStamp

	Block(layout=Vertical Flexible CenterCenter, visible=false) openingModelBlock
		Spinner(mode=Ring, color="secondary")
		Text(value="" , translate=true, format=h3 airTop) openingModelMessage

	Block(layout=Vertical Flexible CenterCenter, visible=false) notFoundBlock
		Block(layout=Vertical Flexible CenterJustified, spacing=DP16, format=relative) as Relative(height=100%)
			Block(layout=Vertical CenterCenter)
				Image(value="/logo-expanded.png", format=airRight, mobileReduceFactor=75%) as Absolute(width=340px, height=80px)
				Text(value="Model not found", format=h4 airTop textCentered, translate=true) message

	Block(layout=Horizontal Flexible, visible=false) contentBlock
		Block(layout=Vertical, format=greyLightBackground) browserBlock as Relative(width=20%)
			Block(layout=Horizontal, format=headerPanel greyLightBackground leftSpaced rightSpaced)
				Block(layout=Horizontal Flexible) > Text(value="Project", format=h6, translate=true)
				Block(layout=Horizontal Center, format=smallAirLeft)
					MaterialIconButton(title="Settings...", icon="Settings", size=Small) settingsTrigger as Action
					MaterialIconButton(title="Operations...", icon="MoreVert", size=Small) operationsTrigger as OpenPopover(workspaceDialog)
					// TODO MaterialIconButton(title="collapse", icon="Close") as CloseBlock()
			DisplayStamp(display=intinoFileBrowser, format=displayHeaderPanel) intinoFileBrowser

		Block(layout=Vertical Flexible)
			Block(layout=Horizontal, format=leftBordered headerPanel greyLightBackground leftSpaced rightSpaced)
				Block(layout=Horizontal Flexible)
					Text(value="", format=h6, translate=true) filename
					Block(layout=Horizontal, visible=false, format=airLeft) fileModifiedMessage > Text(value="modified", translate=true, format=airRight redColor bold)
					Block(layout=Horizontal, visible=false, format=airLeft) fileSavedMessage > Text(value="saved", translate=true, format=airRight greenColor bold)
				Block(layout=Horizontal EndJustified)
			Block(layout=Vertical Flexible CenterCenter, format=whiteBackground) fileNotSelectedBlock
				Text(value="Select file on left panel to open it")
			Block(layout=Vertical Flexible, visible=false) fileSelectedBlock
				DisplayStamp(display=intinoDslEditor, format=fullHeight) intinoDslEditor

	Block(format=spaced) workspaceDialog as Popover Absolute(width=200px)
		Block(layout=Vertical Start)
			MaterialIconButton(title="Add file", icon="Description", titlePosition=Right, size=Small, format=middleAirBottom) addFileTrigger as OpenDialog(fileDialog) > Shortcut(key="N", metaKeys=Control, visible=true)
			MaterialIconButton(title="Add folder", icon="Folder", titlePosition=Right, size=Small, format=middleAirBottom) addFolderTrigger as OpenDialog(fileDialog) > Shortcut(key="N", metaKeys=Shift Control, visible=true)
			MaterialIconButton(title="Rename", icon="Edit", titlePosition=Right, size=Small, format=middleAirBottom) editFilenameTrigger as OpenDialog(fileDialog) Readonly > Shortcut(key="R", metaKeys=Control, visible=true)
			MaterialIconButton(title="Remove", icon="Delete", titlePosition=Right, size=Small, format=middleAirBottom) removeFileTrigger as Action Affirmed("Are you sure to remove?") Readonly

	Dialog(title="") fileDialog as Absolute(width=600px, height=350px)
        Block(layout=Horizontal) as Relative(height=100%, offsetHeight=40px)
        	Block(format=greyLightBackground) selectorBlock as Absolute(width=150px)
				Selector fileDialogSelector as ListBox
					Text(value="New file") newFileOption as Option
					Text(value="Upload file") uploadFileOption as Option
			Block(layout=Vertical Flexible, format=airLeft)
				Block(format=airTop) fileFieldBlock as Conditional(selectorBlock.fileDialogSelector.newFileOption)
					Text fileField as Editable(placeholder="Indicate name") Labeled("Name") Focused
				Block(format=airTop airBottom) fileUploadBlock as Conditional(selectorBlock.fileDialogSelector.uploadFileOption)
					File fileUploadField as Editable(showDropZone=true)
		Divider
        Block(layout=Horizontal EndJustified)
            Button(title="Cancel", size=Small, format=airRight) as CloseDialog(fileDialog)
            Button(title="Accept", size=Small, highlight=Fill) addFile as Action Readonly

	Dialog(title="File modified") fileModifiedDialog as Absolute(width=500px, height=180px)
        Block as Relative(height=100%, offsetHeight=40px)
        	Text(value="File have been modified. What do you want to do?", translate=true)
		Divider
		Block(layout=Horizontal)
			Block(layout=Horizontal Flexible StartJustified)
				Button(title="Cancel", size=Small, format=airRight) cancelSavingFile as Action
			Block(layout=Horizontal EndJustified)
				Button(title="Don't save", size=Small, format=airRight) continueWithoutSavingFile as Action
				Button(title="Save", size=Small, highlight=Fill) continueSavingFile as Action

	TemplateStamp(template=modelSettingsDialog) settingsDialog

Display intinoFileBrowser
	Notification refresh as Object(intinoFileBrowserInfo)
	Notification select as Object(intinoFileBrowserItem)
	Request open as Text
	Request rename as Object(intinoFileBrowserRenameInfo)
	Request move as Object(intinoFileBrowserMoveInfo)

Schema intinoFileBrowserInfo
	Attribute itemAddress as Text
	Attribute items as Object(intinoFileBrowserItem) List

Schema intinoFileBrowserRenameInfo
	Attribute id as Integer
	Attribute newName as Text

Schema intinoFileBrowserMoveInfo
	Attribute file as Integer
	Attribute directory as Text

Schema intinoFileBrowserItem
	Attribute id as Integer
	Attribute name as Text
	Attribute uri as Text
	Attribute type as Word("Folder" "File")
	Attribute isRoot as Bool
	Attribute parents as Text List
	Attribute children as Text List

Display intinoDslEditor
	Notification setup as Object(intinoDslEditorSetup)
	Notification refresh as Object(intinoDslEditorFile)
	Notification receiveContent
	Request fileModified
	Request fileContent as Text

Schema intinoDslEditorSetup
	Attribute dslName as Text
	Attribute modelName as Text
	Attribute modelVersion as Text

Schema intinoDslEditorFile
	Attribute name as Text
	Attribute model as Text
	Attribute uri as Text
	Attribute content as Text
	Attribute language as Text
	Attribute extension as Text

