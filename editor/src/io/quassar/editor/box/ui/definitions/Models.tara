dsl Konos

use Editors
use UI
use Theme

Template(layout=Horizontal Center, format=subHeaderStyle) modelHeaderTemplate

	Block(layout=Horizontal Center Flexible, visible=false) contentBlock
		Block(layout=Horizontal Flexible)
			AvatarIconButton(title="", size=Small, format=airRight, visible=false) projectModelSelector as Action
			Link(title="", format=modelViewTitle) title as Action
			Block(layout=Horizontal Center, visible=false, format=doubleAirRight) titleEditorBlock as Absolute(width=360px)
				Text(translate=true, format=modelViewTitle airRight) titleEditor as Editable
				Link(title="Cancel", format=airRight) closeTitleEditor as Action
				Link(title="Save") saveTitleEditor as Action
			Block(layout=Horizontal Center, format=doubleAirLeft)
				SplitButton(title="draft", format=airRight minSelectorWidth, options="draft", defaultOption = "draft", visible=false) releaseSelector as Action
				Button(title="check", size=Small) checkTrigger as Action > Shortcut(key="F9", metaKeys=Control)
				Button(title="commit", format=airRight, visible=false) commitTrigger as Action
				Button(title="forge", format=airRight, visible=false) forgeTrigger as OpenSite(site=empty, target=Blank)
				Button(title="download", format=airRight, visible=false) downloadTrigger as Download
				Button(title="info", format=airRight, visible=false) infoTrigger as Action Readonly
				Button(title="clone", format=airRight, visible=false) cloneTrigger as Action Affirmed("Cloning the model will create a copy and open it in a new browser tab. Do you want to proceed?")

		Block(layout=Horizontal Center EndJustified)
			Image(format=middleAirRight, mobileReduceFactor=75%, colorInvertedWithDarkMode=true) languageLogo as Absolute(height=25px)
			Button(title="", format=airRight) languageHelpTrigger as OpenSite(site=empty, target=Blank)

	TemplateStamp(template=commitModelDialog) commitModelDialog
	TemplateStamp(template=modelSettingsDialog) modelSettingsDialog

Template modelsTemplate as Relative(height=100%, width=100%)

	Block(layout=Horizontal Center, format=middleAirBottom)
		Button(title="New", size=Small, highlight=Outline, visible=false) addModelTrigger as Action
		SearchBox(collections=body.modelList, visible=false) searchBox

	Block(layout=Vertical Flexible) body
		List(itemClass="io.quassar.editor.model.Model") modelList
			noItemsMessage = "No models"
			Mold
				Item(height = 50px) modelItem
					Block(layout=Vertical Flexible, format=doubleBottomSpaced topSpaced bottomBordered)
						Link(title="", format=relative bringToFront h5) label as Action Addressable(EditorElements.model)
						Text description
						Text owner as Labeled("Owner")
						Date createDate as Labeled("Create date")

Template modelTemplate as Relative(height=100%, width=100%)

	Block(layout=Vertical Flexible CenterCenter, visible=false) openingModelBlock
		Spinner(mode=Ring, color="secondary")
		Text(value="" , translate=true, format=h3 airTop) openingModelMessage

	Block(layout=Vertical Flexible CenterCenter, visible=false) notFoundBlock
		Block(layout=Vertical Flexible CenterJustified, spacing=DP16, format=relative) as Relative(height=100%)
			Block(layout=Vertical CenterCenter)
				Image(value="/images/logo-expanded.png", format=airRight, mobileReduceFactor=75%) as Absolute(width=340px, height=80px)
				Text(value="Model not found", format=h4 airTop textCentered, translate=true) message

	Block(layout=Vertical Flexible) contentBlock as Relative(height=100%, width=100%)
		TemplateStamp(template=modelEditor) modelEditor

Template modelEditor as Relative(height=100%, width=100%)
	TemplateStamp(template=modelHeaderTemplate) headerStamp

	Block(layout=Horizontal Flexible, visible=false) contentBlock as Resizable

		Block(layout=Horizontal Flexible) as Relative(width=20%)
			Block(format=tabPanel leftTabPanel autoWidth) tabSelectorBlock as Relative(height=100%)
				Block(format=panelDrawer) tabSelectorDrawer as Drawer(variant=PersistentAndMini) Absolute(width=200px)
					Block(layout=Horizontal Center, format=middleAirLeft middleAirRight middleAirTop) as Absolute(height=30px)
						Block(layout=Horizontal EndJustified Center) as Absolute(height=30px)
							MaterialIconButton(title="open menu", icon="ChevronRight", size=Small, format=airLeft) as OpenDrawer(drawer=tabSelectorDrawer)
							MaterialIconButton(title = "minimize", icon="ChevronLeft", size=Small, format=airLeft) as CloseDrawer(drawer=tabSelectorDrawer)
							Text(value="Tools", format=airLeft, translate=true)
					Divider
					Block(layout=Vertical Flexible) views
						Selector tabSelector as ListBox Addressable(EditorElements.model)
							Block(layout=Horizontal Center) model as Option > MaterialIcon(icon="DescriptionOutlined", title="Model", format=airRight); Text(value="Model", translate=true)
							Block(layout=Horizontal Center) resources as Option > MaterialIcon(icon="AccountTreeOutlined", title="Resources", format=airRight); Text(value="Resources", translate=true)

			Block(layout=Vertical Flexible, format=whiteBackground) browserBlock as Relative(width=100%, height=100%)
				Block(layout=Vertical Flexible) modelBrowserBlock as Conditional(tabSelectorBlock.tabSelectorDrawer.views.tabSelector.model) Relative(height=100%, width=100%) > TemplateStamp(template=modelBrowserTemplate) modelBrowserStamp
				Block(layout=Vertical Flexible) resourcesBrowserBlock as Conditional(tabSelectorBlock.tabSelectorDrawer.views.tabSelector.resources) Relative(height=100%, width=100%) > TemplateStamp(template=resourcesBrowserTemplate) resourcesBrowserStamp

		Block(layout=Vertical Flexible) as Relative(width=80%)
			Block(layout=Horizontal, format=leftBordered headerPanel greyLightBackground leftSpaced rightSpaced)
				Block(layout=Horizontal Flexible)
					Text(value="", format=h6, translate=true) filename
					Block(layout=Horizontal, visible=false, format=airLeft) fileModifiedMessage > Text(value="modified", translate=true, format=airRight redColor bold)
					Block(layout=Horizontal, visible=false, format=airLeft) fileSavedMessage > Text(value="saved", translate=true, format=airRight greenColor bold)
				Block(layout=Horizontal EndJustified)
			Block(layout=Vertical Flexible) as Resizable Relative(height=100%)
				Block(layout=Vertical Flexible) as Relative(height=60%)
					Block(layout=Vertical Flexible CenterCenter, format=whiteBackground) fileNotSelectedBlock
						Text(value="Select file on left panel to open it")
					Block(layout=Vertical Flexible, format=whiteBackground) nonEditableFileBlock
						File fileField
					Block(layout=Vertical Flexible, visible=false) editableFileBlock
						DisplayStamp(display=intinoDslEditor, format=fullHeight) intinoDslEditor
				Block(visible=false) consoleBlock as Relative(height=40%) > TemplateStamp(template=consoleTemplate) console

	Dialog(title="File modified") fileModifiedDialog as Absolute(width=500px, height=180px)
        Block as Relative(height=100%, offsetHeight=40px)
        	Text(value="File have been modified. What do you want to do?", translate=true)
		Divider
		Block(layout=Horizontal)
			Block(layout=Horizontal Flexible StartJustified)
				Button(title="Cancel", size=Small, format=airRight) cancelSavingFile as Action
			Block(layout=Horizontal EndJustified)
				Button(title="Don't save", size=Small, format=airRight) continueWithoutSavingFile as Action
				Button(title="Save", size=Small, highlight=Fill) continueSavingFile as Action

	Dialog(title="Language help") helpDialog as Relative(width=60%, height=80%)
        Block(format=overflowAuto) as Relative(height=100%, offsetHeight=40px)
        	HtmlViewer(format=fullHeight) helpStamp
		Divider
		Block(layout=Horizontal EndJustified)
			Button(title="Close", size=Small, highlight=Fill) as CloseDialog(helpDialog)

Template(layout=Vertical Flexible) modelBrowserTemplate as Relative(height=100%, width=100%)
	Link(title="") removeFileTrigger as Action Affirmed("Are you sure to remove?")

	Block(layout=Horizontal, format=headerPanel greyLightBackground leftSpaced rightSpaced)
		Block(layout=Horizontal Flexible) > Text(value="Model", format=h6, translate=true)
		Block(layout=Horizontal Center, format=smallAirLeft)
			MaterialIconButton(title="Operations...", icon="MoreVert", size=Small) operationsTrigger as Action

	Block(layout=Vertical Flexible)
		Block(layout=Vertical Flexible) > DisplayStamp(display=intinoFileBrowser, format=displayHeaderPanel smallAirRight) fileBrowser
		Block(layout=Vertical End, format=spaced) > File addFileField as Editable(showDropZone=true)

	Dialog(title="") fileDialog as Absolute(width=400px, height=200px)
        Block(layout=Horizontal) as Relative(height=100%, offsetHeight=40px)
			Text fileField as Editable(placeholder="Indicate name") Labeled("Name") Focused
		Divider
        Block(layout=Horizontal EndJustified)
            Button(title="Cancel", size=Small, format=airRight) as CloseDialog(fileDialog)
            Button(title="Accept", size=Small, highlight=Fill) addFile as Action Readonly

Template(layout=Vertical Flexible) resourcesBrowserTemplate as Relative(height=100%, width=100%)
	Link(title="") removeFileTrigger as Action Affirmed("Are you sure to remove?")

	Block(layout=Horizontal, format=headerPanel greyLightBackground leftSpaced rightSpaced)
		Block(layout=Horizontal Flexible) > Text(value="Resources", format=h6, translate=true)
		Block(layout=Horizontal Center, format=smallAirLeft)
			MaterialIconButton(title="Operations...", icon="MoreVert", size=Small) operationsTrigger as Action

	Block(layout=Vertical Flexible)
		Block(layout=Vertical Flexible) > DisplayStamp(display=intinoFileBrowser, format=displayHeaderPanel smallAirRight) fileBrowser
		Block(layout=Vertical End, format=spaced) > File addFileField as Editable(showDropZone=true)

	Dialog(title="") fileDialog as Absolute(width=600px, height=350px)
        Block(layout=Horizontal) as Relative(height=100%, offsetHeight=40px)
        	Block(format=greyLightBackground) selectorBlock as Absolute(width=150px)
				Selector fileDialogSelector as ListBox
					Text(value="New file") newFileOption as Option
					Text(value="Upload file") uploadFileOption as Option
			Block(layout=Vertical Flexible, format=airLeft)
				Block(format=airTop) fileFieldBlock as Conditional(selectorBlock.fileDialogSelector.newFileOption)
					Text fileField as Editable(placeholder="Indicate name") Labeled("Name") Focused
				Block(format=airTop airBottom) fileUploadBlock as Conditional(selectorBlock.fileDialogSelector.uploadFileOption)
					File fileUploadField as Editable(showDropZone=true)
		Divider
        Block(layout=Horizontal EndJustified)
            Button(title="Cancel", size=Small, format=airRight) as CloseDialog(fileDialog)
            Button(title="Accept", size=Small, highlight=Fill) addFile as Action Readonly

Display intinoFileBrowser
	Notification refresh as Object(intinoFileBrowserInfo)
	Notification select as Object(intinoFileBrowserItem)
	Notification openContextMenu as Object(intinoFileBrowserOperation) List
	Request open as Text
	Request rename as Object(intinoFileBrowserRenameInfo)
	Request move as Object(intinoFileBrowserMoveInfo)
	Request executeOperation as Object(intinoFileBrowserOperationInfo)

Schema intinoFileBrowserInfo
	Attribute itemAddress as Text
	Attribute rootItem as Text
	Attribute hideExtension as Bool
	Attribute historyEnabled as Bool
	Attribute items as Object(intinoFileBrowserItem) List
	Attribute operations as Object(intinoFileBrowserOperation) List

Schema intinoFileBrowserOperationInfo
	Attribute operation as Text
	Attribute target as Integer

Schema intinoFileBrowserRenameInfo
	Attribute id as Integer
	Attribute newName as Text

Schema intinoFileBrowserMoveInfo
	Attribute file as Integer
	Attribute directory as Text

Schema intinoFileBrowserItem
	Attribute id as Integer
	Attribute name as Text
	Attribute uri as Text
	Attribute type as Word("Folder" "File")
	Attribute isRoot as Bool
	Attribute parents as Text List
	Attribute children as Text List

Schema intinoFileBrowserOperation
	Attribute name as Text
	Attribute shortcut as Object(intinoFileBrowserOperationShortcut)
	Attribute enabled as Bool

Schema intinoFileBrowserOperationShortcut
	Attribute ctrlKey as Bool
	Attribute shiftKey as Bool
	Attribute altKey as Bool
	Attribute key as Text

Display intinoDslEditor
	Notification setup as Object(intinoDslEditorSetup)
	Notification refresh as Object(intinoDslEditorFile)
	Notification refreshPosition as Object(intinoDslEditorFilePosition)
	Notification receiveContent
	Request fileModified
	Request fileContent as Text
	Request executeCommand as Text

Schema intinoDslEditorSetup
	Attribute dslName as Text
	Attribute modelName as Text
	Attribute modelRelease as Text
	Attribute readonly as Bool

Schema intinoDslEditorFile
	Attribute name as Text
	Attribute model as Text
	Attribute uri as Text
	Attribute content as Text
	Attribute language as Text
	Attribute extension as Text
	Attribute position as Object(intinoDslEditorFilePosition)

Schema intinoDslEditorFilePosition
	Attribute line as Integer
	Attribute column as Integer

Template(layout=Vertical Flexible, format=consoleStyle) consoleTemplate
	MaterialIconButton(title="Close", icon="Clear", size=Small, format=closeIcon) closeButton as Action
	Text(value="Build output", format=h6 airLeft middleAirTop, translate=true)
	Block(layout=Vertical Flexible, format=airTop airLeft airRight middleAirBottom)
		TemplateStamp(template=consoleEntryTemplate) entries as Multiple(noItemsMessage="No messages")

Template(layout=Horizontal Center, format=middleAirBottom) consoleEntryTemplate
	MaterialIcon(icon="Info", format=smallIconSize airRight blueColor, visible=false) infoIcon
	MaterialIcon(icon="Warning", format=smallIconSize airRight orangeColor, visible=false) warningIcon
	MaterialIcon(icon="Error", format=smallIconSize airRight redColor, visible=false) errorIcon
	Link(title="", format=airRight) file as Action Addressable(EditorElements.model)
	Text(format=airRight) location
	Text content

