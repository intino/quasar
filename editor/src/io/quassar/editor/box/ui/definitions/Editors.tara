dsl Konos

use Models
use Users
use UI
use Theme

Template(layout=Horizontal) modelTitleViewer

	Block simpleTitleBlock as Conditional
		Link(title="", format=modelViewTitle upperCase) title as Action

	Block(layout=Horizontal Center) qualifiedTitleBlock as Conditional
		Link(title="", format=modelViewTitle upperCase airRight) project as OpenPopover(projectBlock)
		Text(value=":", format=modelViewTitleSeparator upperCase airRight)
		Link(title="", format=modelViewTitle upperCase) module as OpenPopover(moduleBlock)
		MaterialIconButton(title="Edit name", icon="Edit", size=Small, format=airLeft) editTitleTrigger as Action

	Block(format=popoverStyle) projectBlock as Popover
		TemplateStamp(template=projectViewer) projectViewer

	Block(format=popoverStyle) moduleBlock as Popover
		Text(format=h5 middleAirBottom) moduleBlockTitle
		TemplateStamp(template=projectModuleViewer) moduleViewer

	TemplateStamp(template=modelTitleDialog) titleDialog

Template projectViewer
	TemplateStamp(template=projectModuleViewer) modules as Multiple(noItemsMessage="no modules")

Template(format=airBottom) projectModuleViewer
	Text(format=h5 middleAirBottom, visible=false) moduleName
	Block(format=doubleAirLeft) > TemplateStamp(template=projectModuleEntryViewer) entries as Multiple(noItemsMessage="no models")

Template(layout=Vertical, format=smallAirBottom) projectModuleEntryViewer
	Link(title="", format=h6, visible=false) titleLink as Action
	Text(format=h6 bold, visible=false) titleText

Template(layout=Horizontal) modelTitleDialog

	Dialog(title="Edit name") dialog as Absolute(height=600px, width=700px)
        Block(layout=Vertical) as Relative(height=100%, offsetHeight=40px)
			Selector typeSelector as RadioBox
				Text(value="Simple name") simpleOption as Option
				Text(value="Qualified name") qualifiedOption as Option

			Block(format=doubleAirTop)
				Block simpleTitleBlock as Conditional(typeSelector.simpleOption)
					Text(mode=Uppercase) titleField as Editable

				Block qualifiedTitleBlock as Conditional(typeSelector.qualifiedOption)
					Text(format=airBottom) qualifiedTitleField as Editable(placeholder="Fill project and module") Readonly
					Block(layout=Horizontal Center)
						Block(format=middleAirRight) as Relative(width=50%, offsetWidth=5px)
							Text(value="Project")
							Selector projectSelector as ComboBox(placeholder="Select project")
							Link(title="Add project", format=smallAirTop) addProjectLink as Action
						Block(format=middleAirLeft) as Relative(width=50%, offsetWidth=5px)
							Text(value="Module")
							Selector moduleSelector as ComboBox(placeholder="Select module")
							Link(title="Add module", format=smallAirTop) addModuleLink as Action
		Divider
        Block(layout=Horizontal EndJustified)
            Button(title="Cancel", size=Small, format=airRight) as CloseDialog(dialog)
            Button(title="Save", size=Small, highlight=Fill) save as Action

    Dialog(title="Add") addDialog as Absolute(width=300px, height=170px)
        Block(layout=Vertical) as Relative(height=100%, offsetHeight=40px)
    		Text(mode=Uppercase) addField as Editable
		Divider
        Block(layout=Horizontal EndJustified)
            Button(title="Cancel", size=Small, format=airRight) as CloseDialog(dialog)
            Button(title="Add", size=Small, highlight=Fill) add as Action

Template(layout=Horizontal) commitModelDialog

	Dialog(title="Commit model") dialog as Absolute(height=300px, width=500px)
        Block(layout=Vertical) as Relative(height=100%, offsetHeight=40px)

			Block(layout=Vertical, visible=false) versionPropertiesBlock
				Block(layout=Vertical Flexible CenterCenter, format=bordered topSpaced bottomSpaced)
					Text(value="New version number", format=middleAirBottom)
					Text(format=veryLargeFontSize) version

				Block(layout=Vertical EndJustified, format=doubleAirTop)
					Selector versionTypeSelector as RadioBox
						Text(value="Major version") majorVersionOption as Option
						Text(value="Minor version") minorVersionOption as Option
						Text(value="Revision") revisionOption as Option

		Divider
        Block(layout=Horizontal EndJustified)
            Button(title="Cancel", size=Small, format=airRight) as CloseDialog(dialog)
            Button(title="Commit", size=Small, highlight=Fill) create as Action

Template(layout=Horizontal) downloadModelDialog
	Link(title="") downloadSilently as Download

	Dialog(title="Download") dialog as Absolute(height=300px, width=500px)
        Block(layout=Vertical) as Relative(height=100%, offsetHeight=40px)

			Block(format=doubleAirBottom)
				Text(value="Zip model", translate=true, format=h5 middleAirBottom)
				Link(title="Download") download as Download

			Block(format=doubleAirBottom)
				Text(value="Model url", translate=true, format=h5 middleAirBottom)
				Text modelUrl
				Block(layout=Horizontal)
					Link(title="Copy") copyModelUrl as CopyToClipboard

		Divider
        Block(layout=Horizontal EndJustified)
            Button(title="Close", size=Small, highlight=Fill) as CloseDialog(dialog)

Template(layout=Horizontal) modelSettingsDialog

	Dialog(title="Info") dialog as Absolute(width=700px, height=800px)
        Block(format=overflowAuto) as Relative(height=100%, offsetHeight=40px)
        	Selector settingsTabSelector as Tabs(format=tabsStyle)
        		Text(value="General") generalOption as Option
        		Text(value="Collaborators") collaboratorsOption as Option
        	Block(layout=Vertical Center) as Relative(width=100%)
        		Block(layout=Vertical Flexible, format=doubleTopSpaced) generalBlock as Relative(width=95%) Conditional(settingsTabSelector.generalOption)
					Block
						Text(value="Name", translate=true, format=h5 middleAirBottom)
						Text(mode=Uppercase) modelTitleField as Editable
					Block(format=doubleAirTop)
						Text(value="Description", translate=true, format=h5 middleAirBottom)
						Text modelDescriptionField as Editable Memo(height=4Rows)
					Block(format=doubleAirTop)
						Text(value="Language version", translate=true, format=h5 middleAirBottom)
						Text(format=bold middleAirBottom) languageName
						Selector languageSelector as ComboBox
					Block(format=doubleAirTop)
						Text(value="Model access", translate=true, format=h5 middleAirBottom)
						Switch(title="Model is private") accessTypeField
					Block(format=doubleAirTop)
						Text(value="Remove model", translate=true, format=h5)
						Text(value="Removed models cannot be restored!", format=greyColor airBottom, translate=true)
						Block(layout=Horizontal) > Button(title="Remove model...", highlight=Fill) removeModel as Action Readonly Affirmed("Are you sure to remove model?")

        		Block(layout=Vertical Flexible, format=doubleTopSpaced) collaboratorsBlock as Relative(width=95%) Conditional(settingsTabSelector.collaboratorsOption)
					Text(value="Users that will have access", translate=true, format=h5 middleAirBottom)
					TemplateStamp(template=collaboratorsTemplate) collaboratorsStamp

		Divider
        Block(layout=Horizontal)
        	Block(layout=Horizontal StartJustified Flexible) > Button(title="Close", size=Small, format=airRight) as CloseDialog(dialog)
            Block(layout=Horizontal EndJustified) > Button(title="Apply", size=Small, highlight=Fill) saveSettings as Action

Template(layout=Horizontal Center, format=tag) tagEditor
	Text(format=smallAirRight) nameField
	MaterialIconButton(title="Delete", icon="Clear", size=Small, format=smallIconSize) delete as Action

Template(layout=Vertical) createLanguageEditor
	Block(format=whiteBackground bordered doubleSpaced)
		Text(format=h4) metamodelTitle
		Text(value="There is no DSL associated with this model. Please specify the name you want to assign to it.", format=description doubleAirBottom)
		TemplateStamp(template=languageEditor) editorStamp
	Block(layout=Horizontal StartJustified, format=airTop)
		Block(layout=Horizontal EndJustified) > Button(title="Create", highlight=Fill) create as Action Readonly

Template(layout=Horizontal) languageEditor as Relative(width=100%, height=100%)
	Block(layout=Vertical Flexible, format=doubleAirRight airTop)
		Text(value="DSL", translate=true, format=h5 middleAirBottom)
		Block(format=relative)
			Text(format=dslFieldInput, mode=Lowercase) idField as Editable
			MaterialIcon(icon="Check", color="green", format=veryLargeIconSize rightIconField, visible=false) validIdIcon
			MaterialIcon(icon="Clear", color="red", format=veryLargeIconSize rightIconField, visible=false) invalidIdIcon
		Link(title="Change", visible=false) changeId as Action Readonly Signed(signText="The language name is about to be changed. This operation will affect all generated models. To confirm, please re-enter the new language name.", mode=SimplePassword)
	Block(format=doubleAirLeft relative) as Absolute(width=170px)
		Image logoField as Editable Absolute(height=150px, width=150px)
		Link(title="Generate", format=languageEditorLogoStyle) generateLogo as Action Readonly

Template(layout=Horizontal) languageExecutionLauncher
	Link(title="") siteLauncher as OpenSite(site=empty, target=Blank)

	Dialog(title="Forge instructions") localDialog as Absolute(width=800px, height=600px)
        Block(format=overflowAuto) as Relative(height=100%, offsetHeight=40px)
			Text(value="You need to follow this instructions to execute this model in your local environment", format=middleAirBottom)

			Block(format=relative airTop)
				Text execution as Editable Memo(height=20Rows) Readonly
				Selector(size=Small, format=localLanguageSelectorStyle) localLanguageSelector as ToggleBox
			Block(layout=Horizontal) > Button(title="Copy instructions", format=middleAirTop, highlight=Outline) copy as CopyToClipboard

		Divider
        Block(layout=Horizontal EndJustified)
        	Button(title="Close", size=Small) as CloseDialog(localDialog)
