dsl Konos

use Models
use Users
use UI
use Theme

Template(layout=Horizontal Center) modelTitleViewer

	Block simpleTitleBlock as Conditional
		Text(format=modelViewTitle upperCase) title

	Block(layout=Horizontal Center) qualifiedTitleBlock as Conditional
		Link(title="", format=modelViewTitle upperCase airRight) project as OpenPopover(projectBlock)
		Text(value=":", format=modelViewTitleSeparator upperCase airRight)
		Link(title="", format=modelViewTitle upperCase) module as OpenPopover(moduleBlock)

	Block(format=popoverStyle) projectBlock as Popover
		TemplateStamp(template=projectViewer) projectViewer

	Block(format=popoverStyle) moduleBlock as Popover
		Block(format=middleAirTop middleAirBottom) moduleBlockHeader > Text(format=h5) moduleBlockTitle
		TemplateStamp(template=projectModuleViewer) moduleViewer

Template projectViewer
	TemplateStamp(template=projectModuleViewer) modules as Multiple(noItemsMessage="no modules")

Template(format=airBottom) projectModuleViewer
	Text(format=h5 middleAirBottom, visible=false) moduleName
	Block(format=doubleAirLeft) > TemplateStamp(template=projectModuleEntryViewer) entries as Multiple(noItemsMessage="no models")

Template(layout=Vertical, format=smallAirBottom) projectModuleEntryViewer
	Link(title="", format=h6, visible=false) titleLink as Action
	Text(format=h6 bold, visible=false) titleText

Template(layout=Horizontal) modelTitleDialog

	Dialog(title="Edit name") dialog as Absolute(height=600px, width=800px)
        Block(layout=Vertical) as Relative(height=100%, offsetHeight=40px)
			Selector typeSelector as RadioBox
				Text(value="Simple name") simpleOption as Option
				Text(value="Qualified name") qualifiedOption as Option

			Block(format=doubleAirTop)
				Block simpleTitleBlock as Conditional(typeSelector.simpleOption)
					Text(mode=Uppercase) titleField as Editable

				Block qualifiedTitleBlock as Conditional(typeSelector.qualifiedOption)
					Text(format=airBottom) qualifiedTitleField as Editable(placeholder="Fill project and module") Readonly
					Block(layout=Horizontal Center)
						Block(format=middleAirRight) as Relative(width=50%, offsetWidth=5px)
							Text(value="Project")
							Selector projectSelector as ComboBox(placeholder="Select project")
							Link(title="Add project", format=smallAirTop) addProjectLink as Action
						Block(format=middleAirLeft) as Relative(width=50%, offsetWidth=5px)
							Text(value="Module")
							Selector moduleSelector as ComboBox(placeholder="Select module")
							Link(title="Add module", format=smallAirTop) addModuleLink as Action
		Divider
        Block(layout=Horizontal EndJustified)
            Button(title="Cancel", size=Small, format=airRight) as CloseDialog(dialog)
            Button(title="Save", size=Small, highlight=Fill) save as Action

    Dialog(title="Add") addDialog as Absolute(width=300px, height=170px)
        Block(layout=Vertical) as Relative(height=100%, offsetHeight=40px)
    		Text(mode=Uppercase) addField as Editable
		Divider
        Block(layout=Horizontal EndJustified)
            Button(title="Cancel", size=Small, format=airRight) as CloseDialog(dialog)
            Button(title="Add", size=Small, highlight=Fill) add as Action

Template(layout=Horizontal) commitModelDialog

	Dialog(title="Commit model") dialog as Absolute(height=300px, width=500px)
        Block(layout=Vertical) as Relative(height=100%, offsetHeight=40px)

			Block(layout=Vertical, visible=false) versionPropertiesBlock
				Block(layout=Vertical Flexible CenterCenter, format=bordered topSpaced bottomSpaced)
					Text(value="New version number", format=middleAirBottom)
					Text(format=veryLargeFontSize) version

				Block(layout=Vertical EndJustified, format=doubleAirTop)
					Selector versionTypeSelector as RadioBox

		Divider
        Block(layout=Horizontal EndJustified)
            Button(title="Cancel", size=Small, format=airRight) as CloseDialog(dialog)
            Button(title="Commit", size=Small, highlight=Fill) create as Action

	TemplateStamp(template=downloadModelDialog) downloadModelDialog
	TemplateStamp(template=languageExecutionLauncher) executionLauncher

Template(layout=Horizontal) downloadModelDialog
	Link(title="") downloadSilently as Download

	Dialog(title="Download") dialog as Absolute(height=300px, width=550px)
        Block(layout=Vertical) as Relative(height=100%, offsetHeight=40px)

			Block(format=doubleAirBottom)
				Text(value="Model file (zip format)", translate=true, format=h5 middleAirBottom)
				Link(title="Download") download as Download

			Block(format=doubleAirBottom)
				Text(value="Model url", translate=true, format=h5 middleAirBottom)
				Text modelUrl
				Block(layout=Vertical, format=airTop)
					Link(title="Copy URL", format=smallAirBottom) copyReleaseUrl as CopyToClipboard
					Link(title="Copy commit") copyReleaseCommit as CopyToClipboard

		Divider
        Block(layout=Horizontal EndJustified)
            Button(title="Close", size=Small, highlight=Fill) as CloseDialog(dialog)

Template modelSettingsEditor

	Selector settingsTabSelector as Tabs(format=tabsStyle)
		Text(value="General") generalOption as Option
		Text(value="Collaborators") collaboratorsOption as Option

	Block(layout=Vertical, format=doubleAirLeft) as Relative(width=60%)
		Block(layout=Vertical Flexible, format=doubleTopSpaced) generalBlock as Relative(width=95%) Conditional(settingsTabSelector.generalOption)
			Block
				Text(value="Name", translate=true, format=h5 middleAirBottom)
				Block(layout=Horizontal Center)
					Text(mode=Uppercase, format=airRight) modelTitleField
					Button(title="Edit", size=Small) editTitle as Action
					TemplateStamp(template=modelTitleDialog) titleDialog
			Block(format=doubleAirTop)
				Text(value="Description", translate=true, format=h5 middleAirBottom)
				Text modelDescriptionField as Editable Memo(height=4Rows)
			Block(format=doubleAirTop)
				Text(value="DSL version", translate=true, format=h5 middleAirBottom)
				Text(format=bold middleAirBottom) languageName
				Selector languageSelector as ComboBox
			Block(format=doubleAirTop, visible=false) accessTypeBlock
				Text(value="Model access", translate=true, format=h5 middleAirBottom)
				Switch(title="Model is private") accessTypeField
			Block(format=doubleAirTop)
				Text(value="Remove model", translate=true, format=h5)
				Text(value="Removed models cannot be restored!", format=greyColor airBottom, translate=true)
				Block(layout=Horizontal) > Button(title="Remove model...", highlight=Fill) removeModel as Action Readonly Affirmed("Are you sure to remove model?")
			Block(format=doubleAirTop)
				Text(value="Clone model", translate=true, format=h5)
				Text(value="Copy both model files and resource files to a new model", format=greyColor airBottom, translate=true)
				Block(layout=Horizontal) > Button(title="Clone model...", highlight=Fill) cloneModel as Action Affirmed("Cloning the model will create a copy and open it. Do you want to proceed?")

		Block(layout=Vertical Flexible, format=doubleTopSpaced) collaboratorsBlock as Relative(width=95%) Conditional(settingsTabSelector.collaboratorsOption)
			Text(value="Users that will have access", translate=true, format=h5 middleAirBottom)
			TemplateStamp(template=collaboratorsTemplate) collaboratorsStamp

Template(layout=Horizontal Center, format=tag) tagEditor
	Text(format=smallAirRight) nameField
	MaterialIconButton(title="Delete", icon="Clear", size=Small, format=smallIconSize) delete as Action

Template(layout=Vertical) createLanguageEditor
	Block(format=whiteBackground bordered doubleSpaced)
		Text(format=h4) metamodelTitle
		Text(value="There is no DSL associated with this model. Please specify the name you want to assign to it.", format=description doubleAirBottom)
		TemplateStamp(template=languageEditor) editorStamp
	Block(layout=Horizontal StartJustified, format=airTop)
		Block(layout=Horizontal EndJustified) > Button(title="Create", highlight=Fill) create as Action Readonly

Template(layout=Horizontal) languageEditor as Relative(width=100%, height=100%)
	Block(layout=Vertical Flexible, format=doubleAirRight airTop)
		Text(value="DSL", translate=true, format=h5 middleAirBottom)
		Block(format=relative)
			Text(format=dslFieldInput, mode=Lowercase) idField as Editable
			MaterialIcon(icon="Check", color="green", format=veryLargeIconSize rightIconField, visible=false) validIdIcon
			MaterialIcon(icon="Clear", color="red", format=veryLargeIconSize rightIconField, visible=false) invalidIdIcon
		Link(title="Change", visible=false) changeId as Action Readonly Signed(signText="The DSL name is about to be changed. This operation will affect all generated models. To confirm, please re-enter the new DSL name.", mode=SimplePassword)
	Block(format=doubleAirLeft relative) as Absolute(width=170px)
		Image logoField as Editable Absolute(height=150px, width=150px)
		Link(title="Generate", format=languageEditorLogoStyle) generateLogo as Action Readonly

Template(layout=Horizontal) languageExecutionLauncher
	Link(title=" ") siteLauncher as OpenSite(site=empty, target=Blank)

	Dialog(title="Execution instructions") localDialog as Absolute(width=800px, height=600px)
        Block(format=overflowAuto) as Relative(height=100%, offsetHeight=40px)
			Text(value="To run this model locally, make sure your environment is set up correctly. Below youâ€™ll find installation notes. Once everything is ready, you can copy and run the command shown to execute the model.", format=middleAirBottom)
			Text(format=airTop) execution as Editable Memo(height=20Rows) Readonly
			Block(layout=Horizontal) > Link(title="Copy", format=middleAirTop upperCase) copy as CopyToClipboard
			Block(layout=Horizontal) > Link(title="Installation notes", format=middleAirTop upperCase, visible=false) openInstallationNotes as OpenSite(site=empty, target=Blank)

		Divider
        Block(layout=Horizontal EndJustified)
        	Button(title="Close", size=Small) as CloseDialog(localDialog)
