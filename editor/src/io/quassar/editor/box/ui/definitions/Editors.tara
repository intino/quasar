dsl Konos

use Models
use Users
use UI
use Theme

Template addModelDialog

	Dialog(title="Add model") dialog as Absolute(height=450px, width=450px)
        Block as Relative(height=100%, offsetHeight=40px)
			Block(format=doubleAirTop)
				Text(value="Title", translate=true, format=h5 middleAirBottom)
				Text titleField as Editable
			Block(format=doubleAirTop)
				Text(value="Description", translate=true, format=h5 middleAirBottom)
				Text descriptionField as Editable Memo(height=4Rows)
		Divider
        Block(layout=Horizontal EndJustified)
            Button(title="Cancel", size=Small, format=airRight) as CloseDialog(dialog)
            Button(title="Add", size=Small, highlight=Fill) create as Action

Template(layout=Horizontal) commitModelDialog

	Dialog(title="Commit model") dialog as Absolute(height=350px, width=650px)
        Block(layout=Vertical) as Relative(height=100%, offsetHeight=40px)

			Block(layout=Vertical, visible=false) versionPropertiesBlock
				Block(layout=Vertical Flexible CenterCenter, format=bordered topSpaced bottomSpaced)
					Text(value="New version number", format=middleAirBottom)
					Text(format=veryLargeFontSize) version

				Block(layout=Vertical EndJustified, format=doubleAirTop)
					Selector versionTypeSelector as RadioBox
						Text(value="Major version") majorVersionOption as Option
						Text(value="Minor version") minorVersionOption as Option
						Text(value="Revision") revisionOption as Option

		Divider
        Block(layout=Horizontal EndJustified)
            Button(title="Cancel", size=Small, format=airRight) as CloseDialog(dialog)
            Button(title="Commit", size=Small, highlight=Fill) create as Action

Template(layout=Horizontal) modelSettingsDialog

	Dialog(title="Info") dialog as Relative(width=50%, height=70%) Modal
        Block(format=overflowAuto) as Relative(height=100%, offsetHeight=40px)
        	Selector settingsTabSelector as Tabs(format=tabsStyle)
        		Text(value="General") generalOption as Option
        		Text(value="Collaborators") collaboratorsOption as Option
        	Block(layout=Vertical Center) as Relative(width=100%)
        		Block(layout=Vertical Flexible, format=doubleTopSpaced) generalBlock as Relative(width=95%) Conditional(settingsTabSelector.generalOption)
					Block(format=doubleAirTop)
						Text(value="Title", translate=true, format=h5 middleAirBottom)
						Text modelTitleField as Editable
					Block(format=doubleAirTop)
						Text(value="Description", translate=true, format=h5 middleAirBottom)
						Text modelDescriptionField as Editable Memo(height=4Rows)
					Block(format=doubleAirTop)
						Text(value="Model access", translate=true, format=h5 middleAirBottom)
						Switch(title="Model is private") accessTypeField
					Block(format=doubleAirTop)
						Text(value="Remove model", translate=true, format=h5)
						Text(value="Removed models cannot be restored!", format=greyColor airBottom, translate=true)
						Block(layout=Horizontal) > Button(title="Remove model...", format=airRight redBackground whiteColor, highlight=Fill) removeModel as Action Readonly Affirmed("Are you sure to remove model?")

        		Block(layout=Vertical Flexible, format=doubleTopSpaced) collaboratorsBlock as Relative(width=95%) Conditional(settingsTabSelector.collaboratorsOption)
					Text(value="Users that will have access", translate=true, format=h5 middleAirBottom)
					TemplateStamp(template=collaboratorsTemplate) collaboratorsStamp

		Divider
        Block(layout=Horizontal)
        	Block(layout=Horizontal StartJustified Flexible) > Button(title="Close", size=Small, format=airRight) as CloseDialog(dialog)
            Block(layout=Horizontal EndJustified) > Button(title="Apply", size=Small, highlight=Fill) saveSettings as Action

Template(layout=Horizontal) languageSettingsDialog

	Dialog(title="Properties") dialog as Relative(width=60%, height=90%) Modal
        Block(format=overflowAuto) as Relative(height=100%, offsetHeight=40px)
        	Selector settingsTabSelector as Tabs(format=tabsStyle)
        		Text(value="General") generalOption as Option
        		Text(value="Help") helpOption as Option
        		Text(value="Language access") accessOption as Option
        	Block(layout=Vertical Center) as Relative(width=100%)
        		Block(layout=Vertical Flexible, format=doubleTopSpaced) generalBlock as Relative(width=95%) Conditional(settingsTabSelector.generalOption)
					TemplateStamp(template=languageEditor) languageEditor

        		Block(layout=Vertical Flexible, format=doubleTopSpaced) helpBlock as Relative(width=95%) Conditional(settingsTabSelector.helpOption)
					Text(format=fullHeight fullWidth) helpField as Editable Code(Html)

        		Block(layout=Vertical Flexible, format=doubleTopSpaced) accessBlock as Relative(width=95%) Conditional(settingsTabSelector.accessOption)
					Text(value="Access patterns", translate=true, format=h5)
					Text(value="List of patterns separated by semicolon with access to this language", translate=true, format=message middleAirBottom)
					Text(format=doubleAirBottom airTop) patternField as Editable(placeholder="Indicate patterns") Labeled("Patterns") Memo(height=5Rows) Focused

		Divider
        Block(layout=Horizontal)
        	Block(layout=Horizontal StartJustified Flexible) > Button(title="Close", size=Small, format=airRight) as CloseDialog(dialog)
            Block(layout=Horizontal EndJustified) > Button(title="Apply", size=Small, highlight=Fill) saveSettings as Action

Template(layout=Horizontal Center, format=tag) tagEditor
	Text(format=smallAirRight) nameField
	MaterialIconButton(title="Delete", icon="Clear", size=Small, format=smallIconSize) delete as Action

Template(layout=Vertical) createLanguageEditor

	Block(format=whiteBackground bordered doubleSpaced)
		Text(format=h4) metamodelTitle
		Text(value="There is no DSL associated with this model. Please specify the name you want to assign to it.", format=description doubleAirBottom)
		Block(layout=Horizontal)
			Block(layout=Vertical Flexible, format=doubleAirRight airTop)
				Text(value="Name", translate=true, format=h5 bold middleAirBottom)
				Text(format=dslNameFieldInput) nameField as Editable Focused
			Block(format=doubleAirLeft) as Absolute(width=170px)
				Image logoField as Editable Absolute(height=150px, width=150px)
	Block(layout=Horizontal StartJustified, format=airTop)
		Button(title="Create", highlight=Fill) create as Action

Template(layout=Horizontal) languageEditor as Relative(width=100%, height=100%)
	Block as Absolute(width=170px)
		Image logoField as Editable Absolute(height=150px, width=150px)
	Block(layout=Vertical Flexible, format=doubleAirLeft)
		Block
			Text(value="Title", translate=true, format=h5 middleAirBottom)
			Text titleField as Editable Focused
		Block(format=doubleAirTop)
			Text(value="Description", translate=true, format=h5 middleAirBottom)
			Text descriptionField as Editable Memo(height=4Rows)
		Block(format=doubleAirTop)
			Text(value="Tags", translate=true, format=h5 middleAirBottom)
			TemplateStamp(template=tagEditor) tags as Multiple("No tags", arrangement=Horizontal, spacing=DP8)
			Block(layout=Horizontal, format=airTop) > Button(title="Add tag", size=Small, highlight=Outline) as OpenDialog(addTagDialog)
		Block(format=doubleAirTop)
			Text(value="Remove language", translate=true, format=h5)
			Text(value="Removed languages cannot be restored!", format=greyColor airBottom, translate=true)
			Block(layout=Horizontal) > Button(title="Remove language...", format=airRight redBackground whiteColor, highlight=Fill) removeLanguage as Action Readonly Affirmed("Are you sure to remove language?")

	Dialog(title="Add tag") addTagDialog as Absolute(width=280px, height=200px)
        Block as Relative(height=100%, offsetHeight=40px)
        	Text tagField as Editable(placeholder="Indicate tag name") Labeled("Tag") Focused
		Divider
        Block(layout=Horizontal EndJustified)
            Button(title="Cancel", size=Small, format=airRight) as CloseDialog(addTagDialog)
            Button(title="Accept", size=Small, highlight=Fill) addTag as Action
