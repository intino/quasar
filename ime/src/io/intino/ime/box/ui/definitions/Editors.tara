dsl Konos

use UI
use Theme

Template modelDialog

	Dialog(title="Start modeling") dialog as Absolute(height=360px, width=400px)
        Block as Relative(height=100%, offsetHeight=40px)
			Block(format=doubleAirBottom)
				Text(value="Language", translate=true, format=labelColor)
				Text(visible=false) languageTitle
				Selector(visible=false) languageSelector as CollectionBox(placeholder="Select language")
					Table(itemClass="io.intino.ime.model.Language") languageTable as Selectable
						noItemsMessage = "No languages"
						Mold
							Heading lteLogoHeading
							Item(height=30px, width=10%) lteLogoItem > Image logo as Absolute(height=25px, width=25px)
						Mold
							Heading lteNameHeading > Text(value="Name", translate=true)
							Item(height=30px, width=20%) lteNameItem > Text name
						Mold
							Heading lteDescriptionHeading > Text(value="Description", translate=true)
							Item(height=30px, width=40%) lteDescriptionItem > Text(cropWithEllipsis=40 Chars) description
						Mold
							Heading lteOwnerHeading > Text(value="Owner", translate=true)
							Item(height=30px, width=30%) lteOwnerItem > Text owner
			Block(format=doubleAirBottom, visible=false) titleBlock > Text(value="Title", format=labelColor, translate=true); Text titleField as Editable
        	Block(format=doubleAirBottom, visible=false) programmingLanguageBlock > Text(value="Programming language", format=labelColor, translate=true); Selector programmingLanguageSelector as ComboBox(placeholder="Select programming language")
        	Block(format=doubleAirBottom, visible=false) scaffoldBlock > Text(value="Build system", format=labelColor, translate=true); Selector scaffoldSelector as ComboBox(placeholder="Select build system")
		Divider
        Block(layout=Horizontal EndJustified)
            Button(title="Cancel", size=Small, format=airRight) as CloseDialog(dialog)
            Button(title="Start", size=Small, highlight=Fill) create as Action

Template(layout=Horizontal) languageDialog

	Dialog(title="Create language") dialog as Absolute(height=490px, width=600px)
        Block(layout=Horizontal) as Relative(height=100%, offsetHeight=40px)
			Block as Absolute(width=170px)
				Image(format=doubleAirBottom) logoField as Editable Absolute(height=150px, width=150px)
			Block(layout=Vertical Flexible)
				Block(format=doubleAirBottom) > Text(value="Name", format=labelColor, translate=true); Text nameField as Editable(helperText="Only alphanumeric characters are allowed") Focused
				Block(format=doubleAirBottom) > Text(value="Group", format=labelColor, translate=true); Text groupField as Editable
				Block(format=doubleAirBottom) programmingLanguageBlock > Text(value="Programming language", format=labelColor, translate=true); Selector programmingLanguageSelector as ComboBox(placeholder="Select programming language")
				Block(format=doubleAirBottom) scaffoldBlock > Text(value="Build system", format=labelColor, translate=true); Selector scaffoldSelector as ComboBox(placeholder="Select build system")
				Block(format=doubleAirBottom) > Text(value="Title", format=labelColor, translate=true); Text descriptionField as Editable
				Switch(title="Language is private", format=doubleAirBottom, state=On) accessTypeField
		Divider
        Block(layout=Horizontal EndJustified)
            Button(title="Cancel", size=Small, format=airRight) as CloseDialog(dialog)
            Button(title="Create", size=Small, highlight=Fill) create as Action

Template userSettingsEditor
	Block(format=doubleAirBottom) credentialsBlock
		Text(value="Credentials used in Quassar", translate=true, format=h5 airBottom)
		Block(layout=Vertical, format=doubleSpaced whiteBackground rounded airBottom)
			Image(format=airBottom, colorInvertedWithDarkMode=true) dockerHubImage as Absolute(width=120px, height=27px)
			Text(value="Go to your settings in Docker Hub to create a new personal access token for Quassar", translate=true, format=greyColor airBottom)
			Text dockerHubTokenField as Editable Password Labeled("Personal Access Token") Focused
		Block(layout=Vertical, format=doubleSpaced whiteBackground rounded airBottom)
			Block(layout=Horizontal Center)
				Image(format=airBottom, colorInvertedWithDarkMode=true) githubImage as Absolute(width=37px, height=37px)
				Text(value="github", format=airLeft h3)
			Text gitHubTokenField as Editable Password Labeled("Personal Access Token") Focused

Template languageSettingsEditor
	Selector(format=tabsStyle) tabSelector as Tabs
		Text(value="General", translate=true) propertiesOption as Option
		Text(value="Builder", translate=true) builderOption as Option

	Block(layout=Horizontal Flexible, format=doubleAirTop) propertiesBlock as Conditional(tabSelector.propertiesOption)
		Block as Absolute(width=170px)
			Image(format=doubleAirBottom) logoField as Editable Absolute(height=150px, width=150px)
		Block(layout=Vertical Flexible)
			Block(format=doubleAirBottom) > Text(value="Name", translate=true, format=labelColor); Text nameField
			Block(format=doubleAirBottom) > Text(value="Group", translate=true, format=labelColor); Text groupField as Editable Focused
			Block(format=doubleAirBottom) > Text(value="Description", translate=true, format=labelColor); Text descriptionField as Editable
			Switch(title="Language is private", format=doubleAirBottom) accessTypeField
			Block(format=doubleAirBottom)
				Text(value="Tags", translate=true, format=h5 middleAirBottom)
				TemplateStamp(template=tagEditor) tags as Multiple("No tags", arrangement=Horizontal, spacing=DP8)
				Block(layout=Horizontal, format=airTop) > Button(title="Add tag", size=Small, highlight=Outline) as OpenDialog(addTagDialog)
			Block(format=doubleAirBottom)
				Text(value="Remove language", translate=true, format=h5 middleAirBottom)
				Text(value="Removed languages cannot be restored!", format=greyColor airBottom, translate=true)
				Block(layout=Horizontal) > Button(title="Remove language...", format=airRight redBackground whiteColor, highlight=Fill) removeLanguage as Action Readonly Affirmed("Are you sure to remove language?")

	Block builderBlock as Conditional(tabSelector.builderOption)
		Block(format=airTop doubleAirBottom)
			Block(layout=Horizontal Center)
				Text builderField as Editable(placeholder="[username]/[builder]:[version]") Labeled("Docker image name")
				MaterialIcon(icon="CheckCircle", format=airTop greenColor, visible=false) validBuilderIcon
				MaterialIcon(icon="Help", format=airTop, visible=false) noBuilderInfoIcon
				MaterialIcon(icon="Cancel", format=airTop redColor, visible=false) invalidBuilderIcon
			Block(layout=Horizontal, format=airTop)
				Button(title="Check", highlight=Outline, size=Small, format=airRight) checkBuilder as Readonly
		Block(visible=false) loadingBlock
			Text(value="Loading builder information...", format=orangeColor) builderState
		Block(visible=false) builderInfoBlock
			Block(format=doubleAirBottom) operationsBlock
				Text(value="Operations", format=h5, translate=true)
				TemplateStamp(template=operationEditor) operations as Multiple("No operations defined")
			Block(format=doubleAirBottom) builderDetailsBlock
				Text(value="Details", format=h5 airBottom, translate=true)
				Text(visible=false) errorMessage
				Block(visible=false) builderPropertiesBlock > TemplateStamp(template=builderPropertyView) builderProperties as Multiple(noItemsMessage="No information")

	Dialog(title="Add tag") addTagDialog as Absolute(width=280px, height=200px)
        Block as Relative(height=100%, offsetHeight=40px)
        	Text tagField as Editable(placeholder="Indicate tag name") Labeled("Tag") Focused
		Divider
        Block(layout=Horizontal EndJustified)
            Button(title="Cancel", size=Small, format=airRight) as CloseDialog(addTagDialog)
            Button(title="Accept", size=Small, highlight=Fill) addTag as Action

Template(layout=Horizontal) modelSettingsDialog
	Button(title="Properties...", format=airRight, size=Small, visible=false) smallIconInList as OpenDialog(settingsDialog)
	Button(title="Properties...", format=airRight, visible=false) largeIconInList as OpenDialog(settingsDialog)
	Button(title="Properties...", format=airRight, size=Small, visible=false) smallIcon as OpenDialog(settingsDialog)
	Button(title="Properties...", format=airRight, visible=false) largeIcon as OpenDialog(settingsDialog)

	Dialog(title="Properties") settingsDialog as Relative(width=60%, height=80%) Modal
        Block(format=overflowAuto) as Relative(height=100%, offsetHeight=40px)
        	Selector settingsTabSelector as Tabs
        		Text(value="General") generalOption as Option
        		Text(value="Git") gitOption as Option
        		Text(value="Build") buildOption as Option
        	Block(layout=Vertical Center) as Relative(width=100%)
        		Block generalBlock as Relative(width=80%) Conditional(settingsTabSelector.generalOption)
					Block
						Text(value="Attributes", translate=true, format=h5 middleAirBottom doubleAirTop)
						Text(format=doubleAirBottom) settingsTitleField as Editable(shrink=true) Labeled("Title")
					Block
						Text(value="Model access", translate=true, format=h5 middleAirBottom doubleAirTop)
						Switch(title="Project is private", format=doubleAirBottom) accessTypeField
					Block
						Text(value="Remove model", translate=true, format=h5 doubleAirTop)
						Text(value="Removed models cannot be restored!", format=greyColor airBottom, translate=true)
						Block(layout=Horizontal) > Button(title="Remove model...", format=airRight redBackground whiteColor, highlight=Fill) removeModel as Action Readonly Affirmed("Are you sure to remove model?")
        		Block gitBlock as Relative(width=80%) Conditional(settingsTabSelector.gitOption)
					Block
						Text(value="Git settings", translate=true, format=h5 middleAirBottom doubleAirTop)
						Text(format=doubleAirBottom) settingsGitUrlField as Editable(shrink=true) Labeled("URL")
						Text(format=doubleAirBottom) settingsGitBranchField as Editable(shrink=true) Labeled("Branch")
        		Block buildBlock as Relative(width=80%) Conditional(settingsTabSelector.buildOption)
					Block
						Text(value="AquÃ­ van los scaffolds")
		Divider
        Block(layout=Horizontal)
        	Block(layout=Horizontal StartJustified Flexible) > Button(title="Close", size=Small, format=airRight) as CloseDialog(settingsDialog)
            Block(layout=Horizontal EndJustified) > Button(title="Apply", size=Small, highlight=Fill) saveSettings as Action

Template(layout=Horizontal) cloneModelDialog
	Button(title="Clone...", format=airRight, size=Small, visible=false) smallIconInList as OpenDialog(modelDialog)
	Button(title="Clone...", format=airRight, visible=false) largeIconInList as OpenDialog(modelDialog)
	Button(title="Clone...", format=airRight, size=Small, visible=false) smallIcon as OpenDialog(modelDialog)
	Button(title="Clone...", format=airRight, visible=false) largeIcon as OpenDialog(modelDialog)

	Dialog(title="Clone project") modelDialog as Absolute(width=400px, height=260px)
        Block as Relative(height=100%, offsetHeight=40px)
        	Text modelTitleField as Editable(placeholder="Indicate title") Labeled("Title") Focused
		Divider
        Block(layout=Horizontal EndJustified)
            Button(title="Cancel", size=Small, format=airRight) as CloseDialog(modelDialog)
            Button(title="Accept", size=Small, highlight=Fill) cloneModel as Action

Template releaseEditor
	Block(format=doubleAirBottom)
		Block(layout=Vertical CenterCenter, format=doubleAirBottom)
			Text(value="New version number", format=middleAirBottom)
			Text(format=largeFontSize) version

		Selector versionTypeSelector as RadioBox
			Text(value="Major version") majorVersionOption as Option
			Text(value="Minor version") minorVersionOption as Option
			Text(value="Revision") revisionOption as Option

	Selector(format=doubleAirBottom) levelSelector as RadioBox Labeled("Language level")
		Text(value="Level 1") level1Option as Option
		Text(value="Level 2") level2Option as Option

Template(layout=Horizontal Center, format=airTop airBottom whiteBackground spaced rounded) operationEditor
	Block(format=airRight) as Absolute(width=200px)
		Text(value="Name", translate=true, format=label)
		Text nameField
	Block
		Text(value="Icon", translate=true, format=label airLeft)
		Block as Absolute(width=270px) > Selector typeField as ComboBox(view=TextView) Focused

	TemplateStamp(template=operationTypeView, visible=false)

Template(layout=Horizontal Center, format=tag) tagEditor
	Text(format=smallAirRight) nameField
	MaterialIconButton(title="Delete", icon="Clear", size=Small, format=smallIconSize) delete as Action

Template(layout=Horizontal Center) operationTypeView
	Block(layout=Vertical Flexible) > Text(format=middleAirRight) nameField
	Block(layout=Horizontal EndJustified) > MaterialIconButton(title="", icon="HelpOutline", size=Small) iconField as Action

Template(layout=Horizontal) builderPropertyView
	Block as Relative(width=25%) > Text name
	Block as Relative(width=75%) > Text value